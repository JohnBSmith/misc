
1. ⊢ A ⊆ B → (∀x. x ∈ A → x ∈ B), bij_eliml incl_equi.
2. 2 ⊢ A ⊆ B, hypo.
3. 2 ⊢ ∀x. x ∈ A → x ∈ B, subj_elim 1 2.
4. 2 ⊢ x ∈ A → x ∈ B, uq_elim 3.
5. ⊢ A ⊆ B → x ∈ A → x ∈ B, subj_intro 4.
incl_elim. (H1 ⊢ A ⊆ B) → (H2 ⊢ x ∈ A) → (H1 ∧ H2 ⊢ x ∈ B), lift_impl_ii 5.

1. ⊢ (∀x. x ∈ A → x ∈ B) → A ⊆ B, bij_elimr incl_equi.
incl_intro. (H ⊢ ∀x. x ∈ A → x ∈ B) → (H ⊢ A ⊆ B), lift_impl 1.

1. ⊢ u ∈ {x | P x} ↔ set u ∧ P u, comp.
2. ⊢ u ∈ {x | P x} → set u ∧ P u, bij_eliml 1.
comp_elim. (H ⊢ u ∈ {x | P x}) → (H ⊢ set u ∧ P u), lift_impl 2.

1. ⊢ u ∈ {x | P x} ↔ set u ∧ P u, comp.
2. ⊢ set u ∧ P u → u ∈ {x | P x}, bij_elimr 1.
comp_intro. (H ⊢ set u ∧ P u) → (H ⊢ u ∈ {x | P x}), lift_impl 2.

ext. (H ⊢ ∀x. x ∈ A ↔ x ∈ B) → (H ⊢ A = B), lift_impl extensionality.

1. 1 ⊢ x ∈ A, hypo.
2. ⊢ x ∈ A → x ∈ A, subj_intro 1.
3. ⊢ ∀x. x ∈ A → x ∈ A, uq_intro 2.
incl_refl. ⊢ A ⊆ A, incl_intro 3.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ B ⊆ C, hypo.
03. 3 ⊢ x ∈ A, hypo.
04. 1, 3 ⊢ x ∈ B, incl_elim 1 3.
05. 1, 2, 3 ⊢ x ∈ C, incl_elim 2 4.
06. 1, 2 ⊢ x ∈ A → x ∈ C, subj_intro 5.
07. 1, 2 ⊢ ∀x. x ∈ A → x ∈ C, uq_intro 6.
08. 1, 2 ⊢ A ⊆ C, incl_intro 7.
09. 1 ⊢ B ⊆ C → A ⊆ C, subj_intro 8.
10. ⊢ A ⊆ B → B ⊆ C → A ⊆ C, subj_intro 9.
incl_trans. (H1 ⊢ A ⊆ B) → (H2 ⊢ B ⊆ C) → (H1 ∧ H2 ⊢ A ⊆ C),
  lift_impl_ii 10.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 1, 2 ⊢ x ∈ B, incl_elim 1 2.
04. 1 ⊢ x ∈ A → x ∈ B, subj_intro 3.
05. 5 ⊢ B ⊆ A, hypo.
06. 6 ⊢ x ∈ B, hypo.
07. 5, 6 ⊢ x ∈ A, incl_elim 5 6.
08. 5 ⊢ x ∈ B → x ∈ A, subj_intro 7.
09. 1, 5 ⊢ x ∈ A ↔ x ∈ B, bij_intro 4 8.
10. 1, 5 ⊢ ∀x. x ∈ A ↔ x ∈ B, uq_intro 9.
11. 1, 5 ⊢ A = B, ext 10.
12. 1 ⊢ B ⊆ A → A = B, subj_intro 11.
13. ⊢ A ⊆ B → B ⊆ A → A = B, subj_intro 12.
incl_antisym. (H1 ⊢ A ⊆ B) → (H2 ⊢ B ⊆ A) → (H1 ∧ H2 ⊢ A = B),
  lift_impl_ii 13.

1. ⊢ (∃C. x ∈ C) → set x, bij_elimr set_equi.
2. 2 ⊢ x ∈ A, hypo.
3. 2 ⊢ ∃C. x ∈ C, ex_intro 2.
4. 2 ⊢ set x, subj_elim 1 3.
5. ⊢ x ∈ A → set x, subj_intro 4.
set_intro. (H ⊢ x ∈ A) → (H ⊢ set x), lift_impl 5.

1. 1 ⊢ x ∈ A ∩ B, hypo.
2. 1 ⊢ x ∈ {x | x ∈ A ∧ x ∈ B}, eq_subst intersection_eq 1, P X ↔ x ∈ X.
3. 1 ⊢ set x ∧ (x ∈ A ∧ x ∈ B), comp_elim 2.
4. 1 ⊢ x ∈ A ∧ x ∈ B, conj_elimr 3.
5. ⊢ x ∈ A ∩ B → x ∈ A ∧ x ∈ B, subj_intro 4.
intersection_elim. (H ⊢ x ∈ A ∩ B) → (H ⊢ x ∈ A ∧ x ∈ B), lift_impl 5.

6. 1 ⊢ x ∈ A, conj_eliml 4.
7. ⊢ x ∈ A ∩ B → x ∈ A, subj_intro 6.
intersection_eliml. (H ⊢ x ∈ A ∩ B) → (H ⊢ x ∈ A), lift_impl 7.

8. 1 ⊢ x ∈ B, conj_elimr 4.
9. ⊢ x ∈ A ∩ B → x ∈ B, subj_intro 8.
intersection_elimr. (H ⊢ x ∈ A ∩ B) → (H ⊢ x ∈ B), lift_impl 9.

1. 1 ⊢ x ∈ A, hypo.
2. 2 ⊢ x ∈ B, hypo.
3. 1, 2 ⊢ x ∈ A ∧ x ∈ B, conj_intro 1 2.
4. 1 ⊢ set x, set_intro 1.
5. 1, 2 ⊢ set x ∧ (x ∈ A ∧ x ∈ B), conj_intro 4 3.
6. 1, 2 ⊢ x ∈ {x | x ∈ A ∧ x ∈ B}, comp_intro 5.
7. 1, 2 ⊢ x ∈ A ∩ B, eq_subst_rev intersection_eq 6, P u ↔ x ∈ u.
8. 1 ⊢ x ∈ B → x ∈ A ∩ B, subj_intro 7.
9. ⊢ x ∈ A → x ∈ B → x ∈ A ∩ B, subj_intro 8.
intersection_intro. (H1 ⊢ x ∈ A) → (H2 ⊢ x ∈ B) → (H1 ∧ H2 ⊢ x ∈ A ∩ B),
  lift_impl_ii 9.
  
01. 1 ⊢ x ∈ A ∨ x ∈ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 2 ⊢ set x, set_intro 2.
04. 1, 2 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), conj_intro 3 1.
05. 1, 2 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, comp_intro 4.
06. 1, 2 ⊢ x ∈ A ∪ B, eq_subst_rev union_eq 5, P u ↔ x ∈ u.
07. 7 ⊢ x ∈ B, hypo.
08. 7 ⊢ set x, set_intro 7.
09. 1, 7 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), conj_intro 8 1.
10. 1, 7 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, comp_intro 9.
11. 1, 7 ⊢ x ∈ A ∪ B, eq_subst_rev union_eq 10, P u ↔ x ∈ u.
12. 1 ⊢ x ∈ A ∪ B, disj_elim 1 6 11.
13. ⊢ x ∈ A ∨ x ∈ B → x ∈ A ∪ B, subj_intro 12.
union_intro. (H ⊢ x ∈ A ∨ x ∈ B) → (H ⊢ x ∈ A ∪ B), lift_impl 13.

1. 1 ⊢ x ∈ A, hypo.
2. 1 ⊢ x ∈ A ∨ x ∈ B, disj_introl 1.
3. 1 ⊢ x ∈ A ∪ B, union_intro 2.
4. ⊢ x ∈ A → x ∈ A ∪ B, subj_intro 3.
union_introl. (H ⊢ x ∈ A) → (H ⊢ x ∈ A ∪ B), lift_impl 4.

1. 1 ⊢ x ∈ B, hypo.
2. 1 ⊢ x ∈ A ∨ x ∈ B, disj_intror 1.
3. 1 ⊢ x ∈ A ∪ B, union_intro 2.
4. ⊢ x ∈ B → x ∈ A ∪ B, subj_intro 3.
union_intror. (H ⊢ x ∈ B) → (H ⊢ x ∈ A ∪ B), lift_impl 4.

1. 1 ⊢ x ∈ A ∪ B, hypo.
2. 1 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, eq_subst union_eq 1, P u ↔ x ∈ u.
3. 1 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), comp_elim 2.
4. 1 ⊢ x ∈ A ∨ x ∈ B, conj_elimr 3.
5. ⊢ x ∈ A ∪ B → x ∈ A ∨ x ∈ B, subj_intro 4.
union_elim. (H ⊢ x ∈ A ∪ B) → (H ⊢ x ∈ A ∨ x ∈ B), lift_impl 5.

1. 1 ⊢ x ∈ A ∩ B, hypo.
2. 1 ⊢ x ∈ A ∧ x ∈ B, intersection_elim 1.
3. 1 ⊢ x ∈ A, conj_eliml 2.
4. ⊢ x ∈ A ∩ B → x ∈ A, subj_intro 3.
5. ⊢ ∀x. x ∈ A ∩ B → x ∈ A, uq_intro 4.
intersection_incl_left. ⊢ A ∩ B ⊆ A, incl_intro 5.

6. 1 ⊢ x ∈ B, conj_elimr 2.
7. ⊢ x ∈ A ∩ B → x ∈ B, subj_intro 6.
8. ⊢ ∀x. x ∈ A ∩ B → x ∈ B, uq_intro 7.
intersection_incl_right. ⊢ A ∩ B ⊆ B, incl_intro 8.

1. 1 ⊢ x ∈ A, hypo.
2. 1 ⊢ x ∈ A ∪ B, union_introl 1.
3. ⊢ x ∈ A → x ∈ A ∪ B, subj_intro 2.
4. ⊢ ∀x. x ∈ A → x ∈ A ∪ B, uq_intro 3.
union_incl_left. ⊢ A ⊆ A ∪ B, incl_intro 4.

1. 1 ⊢ x ∈ B, hypo.
2. 1 ⊢ x ∈ A ∪ B, union_intror 1.
3. ⊢ x ∈ B → x ∈ A ∪ B, subj_intro 2.
4. ⊢ ∀x. x ∈ B → x ∈ A ∪ B, uq_intro 3.
union_incl_right. ⊢ B ⊆ A ∪ B, incl_intro 4.

01. 1 ⊢ x ∈ A ∩ B, hypo.
02. 1 ⊢ x ∈ A, intersection_eliml 1.
03. 1 ⊢ x ∈ B, intersection_elimr 1.
04. 1 ⊢ x ∈ B ∩ A, intersection_intro 3 2.
05. ⊢ x ∈ A ∩ B → x ∈ B ∩ A, subj_intro 4.
06. ⊢ x ∈ B ∩ A → x ∈ A ∩ B, 5.
07. ⊢ x ∈ A ∩ B ↔ x ∈ B ∩ A, bij_intro 5 6.
08. ⊢ ∀x. x ∈ A ∩ B ↔ x ∈ B ∩ A, uq_intro 7.
intersection_comm. ⊢ A ∩ B = B ∩ A, ext 8.

01. 1 ⊢ x ∈ A ∪ B, hypo.
02. 1 ⊢ x ∈ A ∨ x ∈ B, union_elim 1.
03. 3 ⊢ x ∈ A, hypo.
04. 3 ⊢ x ∈ B ∪ A, union_intror 3.
05. 5 ⊢ x ∈ B, hypo.
06. 5 ⊢ x ∈ B ∪ A, union_introl 5.
07. 1 ⊢ x ∈ B ∪ A, disj_elim 2 4 6.
08. ⊢ x ∈ A ∪ B → x ∈ B ∪ A, subj_intro 7.
09. ⊢ x ∈ B ∪ A → x ∈ A ∪ B, 8.
10. ⊢ x ∈ A ∪ B ↔ x ∈ B ∪ A, bij_intro 8 9.
11. ⊢ ∀x. x ∈ A ∪ B ↔ x ∈ B ∪ A, uq_intro 10.
union_comm. ⊢ A ∪ B = B ∪ A, ext 11.

01. 1 ⊢ x ∈ A, hypo.
02. 2 ⊢ ¬x ∈ B, hypo.
03. 1 ⊢ set x, set_intro 1.
04. 1, 2 ⊢ x ∈ A ∧ ¬x ∈ B, conj_intro 1 2.
05. 1, 2 ⊢ set x ∧ (x ∈ A ∧ ¬x ∈ B), conj_intro 3 4.
06. 1, 2 ⊢ x ∈ {x | x ∈ A ∧ ¬x ∈ B}, comp_intro 5.
07. 1, 2 ⊢ x ∈ A \ B, eq_subst_rev diff_eq 6, P u ↔ x ∈ u.
08. 1 ⊢ ¬x ∈ B → x ∈ A \ B, subj_intro 7.
09. ⊢ x ∈ A → ¬x ∈ B → x ∈ A \ B, subj_intro 8.
diff_intro. (H1 ⊢ x ∈ A) → (H2 ⊢ ¬x ∈ B) → (H1 ∧ H2 ⊢ x ∈ A \ B),
  lift_impl_ii 9.

01. 1 ⊢ x ∈ A \ B, hypo.
02. 1 ⊢ x ∈ {x | x ∈ A ∧ ¬x ∈ B}, eq_subst diff_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ set x ∧ (x ∈ A ∧ ¬x ∈ B), comp_elim 2.
04. 1 ⊢ x ∈ A ∧ ¬x ∈ B, conj_elimr 3.
05. 1 ⊢ x ∈ A, conj_eliml 4.
06. ⊢ x ∈ A \ B → x ∈ A, subj_intro 5.
diff_eliml. (H ⊢ x ∈ A \ B) → (H ⊢ x ∈ A), lift_impl 6.

07. 1 ⊢ ¬x ∈ B, conj_elimr 4.
08. ⊢ x ∈ A \ B → ¬x ∈ B, subj_intro 7.
diff_elimr. (H ⊢ x ∈ A \ B) → (H ⊢ ¬x ∈ B), lift_impl 8.

01. 1 ⊢ x ∈ A \ B, hypo.
02. 1 ⊢ x ∈ A, diff_eliml 1.
03. 1 ⊢ ¬x ∈ B, diff_elimr 1.
04. 1 ⊢ set x, set_intro 2.
05. 1 ⊢ set x ∧ ¬x ∈ B, conj_intro 4 3.
06. 1 ⊢ x ∈ {x | ¬x ∈ B}, comp_intro 5.
07. ⊢ compl B = {x | ¬x ∈ B}, compl_eq.
08. 1 ⊢ x ∈ compl B, eq_subst_rev 7 6, P u ↔ x ∈ u.
09. 1 ⊢ x ∈ A ∩ compl B, intersection_intro 2 8.
10. ⊢ x ∈ A \ B → x ∈ A ∩ compl B, subj_intro 9.

11. 11 ⊢ x ∈ A ∩ compl B, hypo.
12. 11 ⊢ x ∈ A, intersection_eliml 11.
13. 11 ⊢ x ∈ compl B, intersection_elimr 11.
14. 11 ⊢ x ∈ {x | ¬x ∈ B}, eq_subst 7 13, P u ↔ x ∈ u.
15. 11 ⊢ set x ∧ ¬x ∈ B, comp_elim 14.
16. 11 ⊢ ¬x ∈ B, conj_elimr 15.
17. 11 ⊢ x ∈ A \ B, diff_intro 12 16.
18. ⊢ x ∈ A ∩ compl B → x ∈ A \ B, subj_intro 17.

19. ⊢ x ∈ A \ B ↔ x ∈ A ∩ compl B, bij_intro 10 18.
20. ⊢ ∀x. x ∈ A \ B ↔ x ∈ A ∩ compl B, uq_intro 19.
diff_as_inter. ⊢ A \ B = A ∩ compl B, ext 20.

1. 1 ⊢ set x, hypo.
2. 2 ⊢ ∀y. y ∈ M → x ∈ y, hypo.
3. 1, 2 ⊢ set x ∧ ∀y. y ∈ M → x ∈ y, conj_intro 1 2.
4. 1, 2 ⊢ x ∈ {x | ∀y. y ∈ M → x ∈ y}, comp_intro 3.
5. 1, 2 ⊢ x ∈ ⋂M, eq_subst_rev Intersection_eq 4, P A ↔ x ∈ A.
6. 1 ⊢ (∀y. y ∈ M → x ∈ y) → x ∈ ⋂M, subj_intro 5.
7. ⊢ set x → (∀y. y ∈ M → x ∈ y) → x ∈ ⋂M, subj_intro 6.
Intersection_intro. (H1 ⊢ set x) → (H2 ⊢ ∀y. y ∈ M → x ∈ y) →
  (H1 ∧ H2 ⊢ x ∈ ⋂M), lift_impl_ii 7.

1. 1 ⊢ x ∈ ⋂M, hypo.
2. 1 ⊢ x ∈ {x | ∀y. y ∈ M → x ∈ y},
  eq_subst Intersection_eq 1, P A ↔ x ∈ A.
3. 1 ⊢ set x ∧ ∀y. y ∈ M → x ∈ y, comp_elim 2.
4. 1 ⊢ ∀y. y ∈ M → x ∈ y, conj_elimr 3.
5. 1 ⊢ y ∈ M → x ∈ y, uq_elim 4.
6. ⊢ x ∈ ⋂M → y ∈ M → x ∈ y, subj_intro 5.
Intersection_elim. (H1 ⊢ x ∈ ⋂M) → (H2 ⊢ y ∈ M) → (H1 ∧ H2 ⊢ x ∈ y),
  lift_impl_ii 6.

1. 1 ⊢ ∃y. y ∈ M ∧ x ∈ y, hypo.
2. 2 ⊢ y ∈ M ∧ x ∈ y, hypo.
3. 2 ⊢ x ∈ y, conj_elimr 2.
4. 2 ⊢ set x, set_intro 3.
5. 1 ⊢ set x, ex_elim 1 4.
6. 1 ⊢ set x ∧ ∃y. y ∈ M ∧ x ∈ y, conj_intro 5 1.
7. 1 ⊢ x ∈ {x | ∃y. y ∈ M ∧ x ∈ y}, comp_intro 6.
8. 1 ⊢ x ∈ ⋃M, eq_subst_rev Union_eq 7, P u ↔ x ∈ u.
9. ⊢ (∃y. y ∈ M ∧ x ∈ y) → x ∈ ⋃M, subj_intro 8.
Union_intro. (H ⊢ ∃y. y ∈ M ∧ x ∈ y) → (H ⊢ x ∈ ⋃M), lift_impl 9.

1. 1 ⊢ x ∈ ⋃M, hypo.
2. 1 ⊢ x ∈ {x | ∃y. y ∈ M ∧ x ∈ y}, eq_subst Union_eq 1, P u ↔ x ∈ u.
3. 1 ⊢ set x ∧ ∃y. y ∈ M ∧ x ∈ y, comp_elim 2.
4. 1 ⊢ ∃y. y ∈ M ∧ x ∈ y, conj_elimr 3.
5. ⊢ x ∈ ⋃M → ∃y. y ∈ M ∧ x ∈ y, subj_intro 4.
Union_elim. (H ⊢ x ∈ ⋃M) → (H ⊢ ∃y. y ∈ M ∧ x ∈ y), lift_impl 5.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ ⋂B, hypo.
03. 3 ⊢ y ∈ A, hypo.
04. 1, 3 ⊢ y ∈ B, incl_elim 1 3.
05. 1, 2, 3 ⊢ x ∈ y, Intersection_elim 2 4.
06. 1, 2 ⊢ y ∈ A → x ∈ y, subj_intro 5.
07. 1, 2 ⊢ ∀y. y ∈ A → x ∈ y, uq_intro 6.
08. 2 ⊢ set x, set_intro 2.
09. 1, 2 ⊢ x ∈ ⋂A, Intersection_intro 8 7.
10. 1 ⊢ x ∈ ⋂B → x ∈ ⋂A, subj_intro 9.
11. 1 ⊢ ∀x. x ∈ ⋂B → x ∈ ⋂A, uq_intro 10.
12. 1 ⊢ ⋂B ⊆ ⋂A, incl_intro 11.
13. ⊢ A ⊆ B → ⋂B ⊆ ⋂A, subj_intro 12.
Intersection_dec. (H ⊢ A ⊆ B) → (H ⊢ ⋂B ⊆ ⋂A), lift_impl 13.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ ⋃A, hypo.
03. 2 ⊢ ∃y. y ∈ A ∧ x ∈ y, Union_elim 2.
04. 4 ⊢ y ∈ A ∧ x ∈ y, hypo.
05. 4 ⊢ y ∈ A, conj_eliml 4.
06. 4 ⊢ x ∈ y, conj_elimr 4.
07. 1, 4 ⊢ y ∈ B, incl_elim 1 5.
08. 1, 4 ⊢ y ∈ B ∧ x ∈ y, conj_intro 7 6.
09. 1, 4 ⊢ ∃y. y ∈ B ∧ x ∈ y, ex_intro 8.
10. 1, 4 ⊢ x ∈ ⋃B, Union_intro 9.
11. 1, 2 ⊢ x ∈ ⋃B, ex_elim 3 10.
12. 1 ⊢ x ∈ ⋃A → x ∈ ⋃B, subj_intro 11.
13. 1 ⊢ ∀x. x ∈ ⋃A → x ∈ ⋃B, uq_intro 12.
14. 1 ⊢ ⋃A ⊆ ⋃B, incl_intro 13.
15. ⊢ A ⊆ B → ⋃A ⊆ ⋃B, subj_intro 14.
Union_inc. (H ⊢ A ⊆ B) → (H ⊢ ⋃A ⊆ ⋃B), lift_impl 15.

# The universal class and Russell's paradox.

1. 1 ⊢ set x, hypo.
2. ⊢ x = x, eq_refl.
3. 1 ⊢ set x ∧ x = x, conj_intro 1 2.
4. 1 ⊢ x ∈ {x | x = x}, comp_intro 3.
5. 1 ⊢ x ∈ UnivCl, eq_subst_rev UnivCl_eq 4, P A ↔ x ∈ A.
6. ⊢ set x → x ∈ UnivCl, subj_intro 5.
UnivCl_intro. (H ⊢ set x) → (H ⊢ x ∈ UnivCl), lift_impl 6.

1. 1 ⊢ x ∈ UnivCl, hypo.
2. 1 ⊢ set x, set_intro 1.
3. ⊢ x ∈ UnivCl → set x, subj_intro 2.
UnivCl_elim. (H ⊢ x ∈ UnivCl) → (H ⊢ set x), lift_impl 3.

1. 1 ⊢ x ∈ A, hypo.
2. 1 ⊢ set x, set_intro 1.
3. 1 ⊢ x ∈ UnivCl, UnivCl_intro 2.
4. ⊢ x ∈ A → x ∈ UnivCl, subj_intro 3.
5. ⊢ ∀x. x ∈ A → x ∈ UnivCl, uq_intro 4.
incl_UnivCl. ⊢ A ⊆ UnivCl, incl_intro 5.

01. 1 ⊢ R = {x | ¬x ∈ x}, hypo.
02. 2 ⊢ u ∈ R, hypo.
03. 1, 2 ⊢ u ∈ {x | ¬x ∈ x}, eq_subst 1 2.
04. 1, 2 ⊢ set u ∧ ¬u ∈ u, comp_elim 3.
05. 1, 2 ⊢ ¬u ∈ u, conj_elimr 4.
06. 1 ⊢ u ∈ R → ¬u ∈ u, subj_intro 5.
07. 1 ⊢ R ∈ R → ¬R ∈ R, 6.

08. 8 ⊢ set R, hypo.
09. 9 ⊢ ¬R ∈ R, hypo.
10. 8, 9 ⊢ set R ∧ ¬R ∈ R, conj_intro 8 9.
11. 8, 9 ⊢ R ∈ {x | ¬x ∈ x}, comp_intro 10.
12. 1, 8, 9 ⊢ R ∈ R, eq_subst_rev 1 11, P x ↔ R ∈ x.
13. 1, 8 ⊢ ¬R ∈ R → R ∈ R, subj_intro 12.

14. 1, 8 ⊢ R ∈ R ↔ ¬R ∈ R, bij_intro 7 13.
15. ⊢ ¬(R ∈ R ↔ ¬R ∈ R), diag_contra.
16. 1, 8 ⊢ ⊥, neg_elim 15 14.
17. 1 ⊢ ¬set R, neg_intro 16.
18. ⊢ R = {x | ¬x ∈ x} → ¬set R, subj_intro 17.

19. ⊢ DiagCl = {x | ¬x ∈ x} → ¬set DiagCl, 18.
DiagCl_is_proper. ⊢ ¬set DiagCl, subj_elim 19 DiagCl_eq.

01. 1 ⊢ x ∈ DiagCl, hypo.
02. 1 ⊢ set x, set_intro 1.
03. 1 ⊢ x ∈ UnivCl, UnivCl_intro 2.
04. ⊢ x ∈ DiagCl → x ∈ UnivCl, subj_intro 3.
05. ⊢ ∀x. x ∈ DiagCl → x ∈ UnivCl, uq_intro 4.
06. ⊢ DiagCl ⊆ UnivCl, incl_intro 5.
07. ⊢ DiagCl ⊆ UnivCl → set UnivCl → set DiagCl, subset.
08. ⊢ set UnivCl → set DiagCl, subj_elim 7 6.
09. 9 ⊢ set UnivCl, hypo.
10. 9 ⊢ set DiagCl, subj_elim 8 9.
11. 9 ⊢ ⊥, neg_elim DiagCl_is_proper 10.
UnivCl_is_proper. ⊢ ¬set UnivCl, neg_intro 11.

# Singletons, pair sets and pairs

01. 1 ⊢ set a, hypo.
02. 2 ⊢ x ∈ {a}, hypo.
03. 2 ⊢ x ∈ {x | set a → x = a}, eq_subst sg_eq 2, P u ↔ x ∈ u.
04. 2 ⊢ set x ∧ (set a → x = a), comp_elim 3.
05. 2 ⊢ set a → x = a, conj_elimr 4.
06. 1, 2 ⊢ x = a, subj_elim 5 1.
07. 1 ⊢ x ∈ {a} → x = a, subj_intro 6.
08. ⊢ set a → x ∈ {a} → x = a, subj_intro 7.
sg_elim. (H1 ⊢ set a) → (H2 ⊢ x ∈ {a}) → (H1 ∧ H2 ⊢ x = a),
  lift_impl_ii 8.

09. 9 ⊢ set a, hypo.
10. 10 ⊢ x = a, hypo.
11. 9, 10 ⊢ set x, eq_subst_rev 10 9, P u ↔ set u.
12. 10, 9 ⊢ x = a, wk 10.
13. 10 ⊢ set a → x = a, subj_intro 12.
14. 9, 10 ⊢ set x ∧ (set a → x = a), conj_intro 11 13.
15. 9, 10 ⊢ x ∈ {x | set a → x = a}, comp_intro 14.
16. 9, 10 ⊢ x ∈ {a}, eq_subst_rev sg_eq 15, P u ↔ x ∈ u.
17. 9 ⊢ x = a → x ∈ {a}, subj_intro 16.
18. ⊢ set a → x = a → x ∈ {a}, subj_intro 17.
sg_intro. (H1 ⊢ set a) → (H2 ⊢ x = a) → (H1 ∧ H2 ⊢ x ∈ {a}),
  lift_impl_ii 18.

19. 1 ⊢ x ∈ {a} ↔ x = a, bij_intro 7 17.
20. ⊢ set a → (x ∈ {a} ↔ x = a), subj_intro 19.
sg_equi. (H ⊢ set a) → (H ⊢ x ∈ {a} ↔ x = a), lift_impl 20.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ x ∈ ⋂{a}, hypo.
03. ⊢ a = a, eq_refl.
04. 1 ⊢ a ∈ {a}, sg_intro 1 3.
05. 1, 2 ⊢ x ∈ a, Intersection_elim 2 4.
06. 1 ⊢ x ∈ ⋂{a} → x ∈ a, subj_intro 5.
07. 1 ⊢ ∀x. x ∈ ⋂{a} → x ∈ a, uq_intro 6.
08. 1 ⊢ ⋂{a} ⊆ a, incl_intro 7.

09. 9 ⊢ x ∈ a, hypo.
10. 10 ⊢ y ∈ {a}, hypo.
11. 1, 10 ⊢ y = a, sg_elim 1 10.
12. 1, 9, 10 ⊢ x ∈ y, eq_subst_rev 11 9, P u ↔ x ∈ u.
13. 1, 9 ⊢ y ∈ {a} → x ∈ y, subj_intro 12.
14. 1, 9 ⊢ ∀y. y ∈ {a} → x ∈ y, uq_intro 13.
15. 9 ⊢ set x, set_intro 9.
16. 1, 9 ⊢ x ∈ ⋂{a}, Intersection_intro 15 14.
17. 1 ⊢ x ∈ a → x ∈ ⋂{a}, subj_intro 16.
18. 1 ⊢ ∀x. x ∈ a → x ∈ ⋂{a}, uq_intro 17.
19. 1 ⊢ a ⊆ ⋂{a}, incl_intro 18.

20. 1 ⊢ ⋂{a} = a, incl_antisym 8 19.
21. ⊢ set a → ⋂{a} = a, subj_intro 20.
Intersection_sg. (H ⊢ set a) → (H ⊢ ⋂{a} = a), lift_impl 21.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. 4 ⊢ {x} = {y}, hypo.
05. 4 ⊢ ⋂{x} = ⋂{y}, f_equal 4.
06. 1 ⊢ ⋂{x} = x, Intersection_sg 2.
07. 1 ⊢ ⋂{y} = y, Intersection_sg 3.
08. 1, 4 ⊢ x = ⋂{y}, eq_subst 6 5, P u ↔ u = ⋂{y}.
09. 1, 4 ⊢ x = y, eq_subst 7 8, P u ↔ x = u.
10. 1 ⊢ {x} = {y} → x = y, subj_intro 9.
11. ⊢ set x ∧ set y → {x} = {y} → x = y, subj_intro 10.
sg_inj. (H1 ⊢ set x ∧ set y) → (H2 ⊢ {x} = {y}) → (H1 ∧ H2 ⊢ x = y),
  lift_impl_ii 11.

01. 1 ⊢ ¬set A, hypo.
02. 2 ⊢ set A, hypo.
03. 1, 2 ⊢ ⊥, neg_elim 1 2.
04. 1, 2 ⊢ x = A, efq 3.
05. 1 ⊢ set A → x = A, subj_intro 4.
06. 6 ⊢ x ∈ UnivCl, hypo.
07. 6 ⊢ set x, set_intro 6.
08. 1, 6 ⊢ set x ∧ (set A → x = A), conj_intro 7 5.
09. 1, 6 ⊢ x ∈ {x | set A → x = A}, comp_intro 8.
10. ⊢ {A} = {x | set A → x = A}, sg_eq.
11. 1, 6 ⊢ x ∈ {A}, eq_subst_rev 10 9, P u ↔ x ∈ u.
12. 1 ⊢ x ∈ UnivCl → x ∈ {A}, subj_intro 11.
13. 1 ⊢ ∀x. x ∈ UnivCl → x ∈ {A}, uq_intro 12.
14. 1 ⊢ UnivCl ⊆ {A}, incl_intro 13.
15. ⊢ {A} ⊆ UnivCl, incl_UnivCl.
16. 1 ⊢ {A} = UnivCl, incl_antisym 15 14.
17. ⊢ ¬set A → {A} = UnivCl, subj_intro 16.
sg_of_proper_class. (H ⊢ ¬set A) → (H ⊢ {A} = UnivCl), lift_impl 17.

1. 1 ⊢ set x, hypo.
2. ⊢ x = x, eq_refl.
3. 1 ⊢ x ∈ {x}, sg_intro 1 2.
4. ⊢ {x} ⊆ {x, y}, union_incl_left.
5. 1 ⊢ x ∈ {x, y}, incl_elim 4 3.
6. ⊢ set x → x ∈ {x, y}, subj_intro 5.
in21. (H ⊢ set x) → (H ⊢ x ∈ {x, y}), lift_impl 6.

1. 1 ⊢ set y, hypo.
2. ⊢ y = y, eq_refl.
3. 1 ⊢ y ∈ {y}, sg_intro 1 2.
4. ⊢ {y} ⊆ {x, y}, union_incl_right.
5. 1 ⊢ y ∈ {x, y}, incl_elim 4 3.
6. ⊢ set y → y ∈ {x, y}, subj_intro 5.
in22. (H ⊢ set y) → (H ⊢ y ∈ {x, y}), lift_impl 6.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. 1 ⊢ x ∈ {x, y}, in21 2.
05. 1 ⊢ y ∈ {x, y}, in22 3.

06. 6 ⊢ u ∈ ⋂{x, y}, hypo.
07. 1, 6 ⊢ u ∈ x, Intersection_elim 6 4.
08. 1, 6 ⊢ u ∈ y, Intersection_elim 6 5.
09. 1, 6 ⊢ u ∈ x ∩ y, intersection_intro 7 8.
10. 1 ⊢ u ∈ ⋂{x, y} → u ∈ x ∩ y, subj_intro 9.
11. 1 ⊢ ∀u. u ∈ ⋂{x, y} → u ∈ x ∩ y, uq_intro 10.
12. 1 ⊢ ⋂{x, y} ⊆ x ∩ y, incl_intro 11.

13. 13 ⊢ u ∈ x ∩ y, hypo.
14. 13 ⊢ u ∈ x, intersection_eliml 13.
15. 13 ⊢ u ∈ y, intersection_elimr 13.
16. 13 ⊢ set u, set_intro 13.
17. 17 ⊢ a ∈ {x, y}, hypo.
18. 17 ⊢ a ∈ {x} ∨ a ∈ {y}, union_elim 17.
19. 19 ⊢ a ∈ {x}, hypo.
20. 1, 19 ⊢ a = x, sg_elim 2 19.
21. 1, 13, 19 ⊢ u ∈ a, eq_subst_rev 20 14, P x ↔ u ∈ x.
22. 22 ⊢ a ∈ {y}, hypo.
23. 1, 22 ⊢ a = y, sg_elim 3 22.
24. 1, 13, 22 ⊢ u ∈ a, eq_subst_rev 23 15, P x ↔ u ∈ x.
25. 1, 13, 17 ⊢ u ∈ a, disj_elim 18 21 24.
26. 1, 13 ⊢ a ∈ {x, y} → u ∈ a, subj_intro 25.
27. 1, 13 ⊢ ∀a. a ∈ {x, y} → u ∈ a, uq_intro 26.
28. 1, 13 ⊢ u ∈ ⋂{x, y}, Intersection_intro 16 27.
29. 1 ⊢ u ∈ x ∩ y → u ∈ ⋂{x, y}, subj_intro 28.
30. 1 ⊢ ∀u. u ∈ x ∩ y → u ∈ ⋂{x, y}, uq_intro 29.
31. 1 ⊢ x ∩ y ⊆ ⋂{x, y}, incl_intro 30.

32. 1 ⊢ ⋂{x, y} = x ∩ y, incl_antisym 12 31.
33. ⊢ set x ∧ set y → ⋂{x, y} = x ∩ y, subj_intro 32.
Intersection_pair_set. (H ⊢ set x ∧ set y) → (H ⊢ ⋂{x, y} = x ∩ y),
  lift_impl 33.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.

04. 4 ⊢ u ∈ ⋃{x, y}, hypo.
05. 4 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, Union_elim 4.
06. 6 ⊢ a ∈ {x, y} ∧ u ∈ a, hypo.
07. 6 ⊢ a ∈ {x, y}, conj_eliml 6.
08. 6 ⊢ u ∈ a, conj_elimr 6.
09. 6 ⊢ a ∈ {x} ∨ a ∈ {y}, union_elim 7.

10. 10 ⊢ a ∈ {x}, hypo.
11. 1, 10 ⊢ a = x, sg_elim 2 10.
12. 1, 6, 10 ⊢ u ∈ x, eq_subst 11 08, P x ↔ u ∈ x.
13. 1, 6, 10 ⊢ u ∈ x ∪ y, union_introl 12.

14. 14 ⊢ a ∈ {y}, hypo.
15. 1, 14 ⊢ a = y, sg_elim 3 14.
16. 1, 6, 14 ⊢ u ∈ y, eq_subst 15 08, P x ↔ u ∈ x.
17. 1, 6, 14 ⊢ u ∈ x ∪ y, union_intror 16.

18. 1, 6 ⊢ u ∈ x ∪ y, disj_elim 9 13 17.
19. 1, 4 ⊢ u ∈ x ∪ y, ex_elim 5 18.
20. 1 ⊢ u ∈ ⋃{x, y} → u ∈ x ∪ y, subj_intro 19.
21. 1 ⊢ ∀u. u ∈ ⋃{x, y} → u ∈ x ∪ y, uq_intro 20.
22. 1 ⊢ ⋃{x, y} ⊆ x ∪ y, incl_intro 21.

23. 23 ⊢ u ∈ x ∪ y, hypo.
24. 23 ⊢ u ∈ x ∨ u ∈ y, union_elim 23.

25. 25 ⊢ u ∈ x, hypo.
26. 1 ⊢ x ∈ {x, y}, in21 2.
27. 1, 25 ⊢ x ∈ {x, y} ∧ u ∈ x, conj_intro 26 25.
28. 1, 25 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, ex_intro 27.
29. 1, 25 ⊢ u ∈ ⋃{x, y}, Union_intro 28.

30. 30 ⊢ u ∈ y, hypo.
31. 1 ⊢ y ∈ {x, y}, in22 3.
32. 1, 30 ⊢ y ∈ {x, y} ∧ u ∈ y, conj_intro 31 30.
33. 1, 30 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, ex_intro 32.
34. 1, 30 ⊢ u ∈ ⋃{x, y}, Union_intro 33.

35. 1, 23 ⊢ u ∈ ⋃{x, y}, disj_elim 24 29 34.
36. 1 ⊢ u ∈ x ∪ y → u ∈ ⋃{x, y}, subj_intro 35.
37. 1 ⊢ ∀u. u ∈ x ∪ y → u ∈ ⋃{x, y}, uq_intro 36.
38. 1 ⊢ x ∪ y ⊆ ⋃{x, y}, incl_intro 37.

39. 1 ⊢ ⋃{x, y} = x ∪ y, incl_antisym 22 38.
40. ⊢ set x ∧ set y → ⋃{x, y} = x ∪ y, subj_intro 39.
Union_pair_set. (H ⊢ set x ∧ set y) → (H ⊢ ⋃{x, y} = x ∪ y),
  lift_impl 40.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 1, 2 ⊢ x ∈ B, incl_elim 1 2.
04. 1, 2 ⊢ x ∈ A ∩ B, intersection_intro 2 3.
05. 1 ⊢ x ∈ A → x ∈ A ∩ B, subj_intro 4.
06. 1 ⊢ ∀x. x ∈ A → x ∈ A ∩ B, uq_intro 5.
07. 1 ⊢ A ⊆ A ∩ B, incl_intro 6.
08. ⊢ A ∩ B ⊆ A, intersection_incl_left.
09. 1 ⊢ A ∩ B = A, incl_antisym 8 7.
10. ⊢ A ⊆ B → A ∩ B = A, subj_intro 9.
intersection_from_incl. (H ⊢ A ⊆ B) → (H ⊢ A ∩ B = A), lift_impl 10.

01. ⊢ A ⊆ A, incl_refl.
intersection_idem. ⊢ A ∩ A = A, intersection_from_incl 1.

01. ⊢ A ⊆ A, incl_refl.
union_idem. ⊢ A ∪ A = A, axiom.

pair_set_is_set. (H1 ⊢ set x) → (H2 ⊢ set y) → (H1 ∧ H2 ⊢ set {x, y}),
  lift_impl_ii pairing.

01. 1 ⊢ set x, hypo.
02. 1 ⊢ set {x, x}, pair_set_is_set 1 1.
03. ⊢ {x, x} = {x}, union_idem.
04. 1 ⊢ set {x}, eq_subst 3 2, P u ↔ set u.
05. ⊢ set x → set {x}, subj_intro 4.
sg_is_set. (H ⊢ set x) → (H ⊢ set {x}), lift_impl 5.

01. 1 ⊢ ¬set x, hypo.
02. 1 ⊢ {x} = UnivCl, sg_of_proper_class 1.
03. 1 ⊢ ¬set {x}, eq_subst_rev 2 UnivCl_is_proper, P u ↔ ¬set u.
04. ⊢ ¬set x → ¬set {x}, subj_intro 3.
05. ⊢ set {x} → set x, contraposition_rev 4.
set_from_sg. (H ⊢ set {x}) → (H ⊢ set x), lift_impl 5.

01. ⊢ A ⊆ A ∪ B, union_incl_left.
02. ⊢ A ⊆ A ∪ B → set (A ∪ B) → set A, subset.
03. ⊢ set (A ∪ B) → set A, subj_elim 2 1.
setl_from_union. (H ⊢ set (A ∪ B)) → (H ⊢ set A), lift_impl 3.

01. ⊢ B ⊆ A ∪ B, union_incl_right.
02. ⊢ B ⊆ A ∪ B → set (A ∪ B) → set B, subset.
03. ⊢ set (A ∪ B) → set B, subj_elim 2 1.
setr_from_union. (H ⊢ set (A ∪ B)) → (H ⊢ set B), lift_impl 3.

01. 1 ⊢ set (x, y), hypo.
02. ⊢ (x, y) = {{x}, {x, y}}, pair_eq.
03. 1 ⊢ set {{x}, {x, y}}, eq_subst 2 1, P u ↔ set u.
04. 1 ⊢ set {{x}}, setl_from_union 3.
05. 1 ⊢ set {x}, set_from_sg 4.
06. 1 ⊢ set x, set_from_sg 5.
07. ⊢ set (x, y) → set x, subj_intro 6.
setl_from_pair. (H ⊢ set (x, y)) → (H ⊢ set x), lift_impl 7.

08. 1 ⊢ set {{x, y}}, setr_from_union 3.
09. 1 ⊢ set {x, y}, set_from_sg 8.
10. 1 ⊢ set {y}, setr_from_union 9.
11. 1 ⊢ set y, set_from_sg 10.
12. ⊢ set (x, y) → set y, subj_intro 11.
setr_from_pair. (H ⊢ set (x, y)) → (H ⊢ set y), lift_impl 12.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. ⊢ ⋂(x, y) = ⋂(x, y), eq_refl.
05. ⊢ (x, y) = {{x}, {x, y}}, pair_eq.
06. ⊢ ⋂(x, y) = ⋂{{x}, {x, y}}, eq_subst 5 4, P u ↔ ⋂(x, y) = ⋂u.
07. 1 ⊢ set {x}, sg_is_set 2.
08. 1 ⊢ set {x, y}, pair_set_is_set 2 3.
09. 1 ⊢ set {x} ∧ set {x, y}, conj_intro 7 8.
10. 1 ⊢ ⋂{{x}, {x, y}} = {x} ∩ {x, y}, Intersection_pair_set 9.
11. 1 ⊢ ⋂(x, y) = {x} ∩ {x, y}, eq_trans 6 10.
12. ⊢ {x} ⊆ {x, y}, union_incl_left.
13. ⊢ {x} ∩ {x, y} = {x}, intersection_from_incl 12.
14. 1 ⊢ ⋂(x, y) = {x}, eq_trans 11 13.
15. 1 ⊢ ⋂⋂(x, y) = ⋂{x}, f_equal 14.
16. 1 ⊢ ⋂{x} = x, Intersection_sg 2.
17. 1 ⊢ ⋂⋂(x, y) = x, eq_trans 15 16.
18. ⊢ fst (x, y) = ⋂⋂(x, y), fst_eq.
19. 1 ⊢ fst (x, y) = x, eq_trans 18 17.
20. ⊢ set x ∧ set y → fst (x, y) = x, subj_intro 19.
pair_fst. (H ⊢ set x ∧ set y) → (H ⊢ fst (x, y) = x), lift_impl 20.

