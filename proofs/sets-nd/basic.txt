
01. ⊢ A ⊆ B → (∀x. x ∈ A → x ∈ B), bij_eliml incl_equi.
02. 2 ⊢ A ⊆ B, hypo.
03. 2 ⊢ ∀x. x ∈ A → x ∈ B, subj_elim 1 2.
04. 2 ⊢ x ∈ A → x ∈ B, uq_elim 3.
incl_elim. ⊢ A ⊆ B → x ∈ A → x ∈ B, subj_intro 4.

incl_intro. ⊢ (∀x. x ∈ A → x ∈ B) → A ⊆ B, bij_elimr incl_equi.

01. ⊢ u ∈ {x | P x} ↔ set u ∧ P u, comp.
comp_elim. ⊢ u ∈ {x | P x} → set u ∧ P u, bij_eliml 1.

01. ⊢ u ∈ {x | P x} ↔ set u ∧ P u, comp.
comp_intro. ⊢ set u ∧ P u → u ∈ {x | P x}, bij_elimr 1.

01. 1 ⊢ x ∈ A, hypo.
02. ⊢ x ∈ A → x ∈ A, subj_intro 1.
03. ⊢ ∀x. x ∈ A → x ∈ A, uq_intro 2.
incl_refl. ⊢ A ⊆ A, incl_intro 3.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ B ⊆ C, hypo.
03. 3 ⊢ x ∈ A, hypo.
04. 1, 3 ⊢ x ∈ B, incl_elim 1 3.
05. 1, 2, 3 ⊢ x ∈ C, incl_elim 2 4.
06. 1, 2 ⊢ x ∈ A → x ∈ C, subj_intro 5.
07. 1, 2 ⊢ ∀x. x ∈ A → x ∈ C, uq_intro 6.
08. 1, 2 ⊢ A ⊆ C, incl_intro 7.
09. 1 ⊢ B ⊆ C → A ⊆ C, subj_intro 8.
incl_trans. ⊢ A ⊆ B → B ⊆ C → A ⊆ C, subj_intro 9.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 1, 2 ⊢ x ∈ B, incl_elim 1 2.
04. 1 ⊢ x ∈ A → x ∈ B, subj_intro 3.
05. 5 ⊢ B ⊆ A, hypo.
06. 6 ⊢ x ∈ B, hypo.
07. 5, 6 ⊢ x ∈ A, incl_elim 5 6.
08. 5 ⊢ x ∈ B → x ∈ A, subj_intro 7.
09. 1, 5 ⊢ x ∈ A ↔ x ∈ B, bij_intro 4 8.
10. 1, 5 ⊢ ∀x. x ∈ A ↔ x ∈ B, uq_intro 9.
11. 1, 5 ⊢ A = B, ext 10.
12. 1 ⊢ B ⊆ A → A = B, subj_intro 11.
incl_antisym. ⊢ A ⊆ B → B ⊆ A → A = B, subj_intro 12.

01. ⊢ (∃C. x ∈ C) → set x, bij_elimr set_equi.
02. 2 ⊢ x ∈ A, hypo.
03. 2 ⊢ ∃C. x ∈ C, ex_intro 2.
04. 2 ⊢ set x, subj_elim 1 3.
set_intro. ⊢ x ∈ A → set x, subj_intro 4.

01. 1 ⊢ x ∈ A ∩ B, hypo.
02. 1 ⊢ x ∈ {x | x ∈ A ∧ x ∈ B},
  eq_subst intersection_eq 1, P X ↔ x ∈ X.
03. 1 ⊢ set x ∧ (x ∈ A ∧ x ∈ B), comp_elim 2.
04. 1 ⊢ x ∈ A ∧ x ∈ B, conj_elimr 3.
intersection_elim. ⊢ x ∈ A ∩ B → x ∈ A ∧ x ∈ B, subj_intro 4.

06. 1 ⊢ x ∈ A, conj_eliml 4.
intersection_eliml. ⊢ x ∈ A ∩ B → x ∈ A, subj_intro 6.

08. 1 ⊢ x ∈ B, conj_elimr 4.
intersection_elimr. ⊢ x ∈ A ∩ B → x ∈ B, subj_intro 8.

01. 1 ⊢ x ∈ A, hypo.
02. 2 ⊢ x ∈ B, hypo.
03. 1, 2 ⊢ x ∈ A ∧ x ∈ B, conj_intro 1 2.
04. 1 ⊢ set x, set_intro 1.
05. 1, 2 ⊢ set x ∧ (x ∈ A ∧ x ∈ B), conj_intro 4 3.
06. 1, 2 ⊢ x ∈ {x | x ∈ A ∧ x ∈ B}, comp_intro 5.
07. 1, 2 ⊢ x ∈ A ∩ B, eq_subst_rev intersection_eq 6, P u ↔ x ∈ u.
08. 1 ⊢ x ∈ B → x ∈ A ∩ B, subj_intro 7.
intersection_intro. ⊢ x ∈ A → x ∈ B → x ∈ A ∩ B, subj_intro 8.
  
01. 1 ⊢ x ∈ A ∨ x ∈ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 2 ⊢ set x, set_intro 2.
04. 1, 2 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), conj_intro 3 1.
05. 1, 2 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, comp_intro 4.
06. 1, 2 ⊢ x ∈ A ∪ B, eq_subst_rev union_eq 5, P u ↔ x ∈ u.
07. 7 ⊢ x ∈ B, hypo.
08. 7 ⊢ set x, set_intro 7.
09. 1, 7 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), conj_intro 8 1.
10. 1, 7 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, comp_intro 9.
11. 1, 7 ⊢ x ∈ A ∪ B, eq_subst_rev union_eq 10, P u ↔ x ∈ u.
12. 1 ⊢ x ∈ A ∪ B, disj_elim 1 6 11.
union_intro. ⊢ x ∈ A ∨ x ∈ B → x ∈ A ∪ B, subj_intro 12.

01. 1 ⊢ x ∈ A, hypo.
02. 1 ⊢ x ∈ A ∨ x ∈ B, disj_introl 1.
03. 1 ⊢ x ∈ A ∪ B, union_intro 2.
union_introl. ⊢ x ∈ A → x ∈ A ∪ B, subj_intro 3.

01. 1 ⊢ x ∈ B, hypo.
02. 1 ⊢ x ∈ A ∨ x ∈ B, disj_intror 1.
03. 1 ⊢ x ∈ A ∪ B, union_intro 2.
union_intror. ⊢ x ∈ B → x ∈ A ∪ B, subj_intro 3.

01. 1 ⊢ x ∈ A ∪ B, hypo.
02. 1 ⊢ x ∈ {x | x ∈ A ∨ x ∈ B}, eq_subst union_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ set x ∧ (x ∈ A ∨ x ∈ B), comp_elim 2.
04. 1 ⊢ x ∈ A ∨ x ∈ B, conj_elimr 3.
union_elim. ⊢ x ∈ A ∪ B → x ∈ A ∨ x ∈ B, subj_intro 4.

01. 1 ⊢ x ∈ A ∩ B, hypo.
02. 1 ⊢ x ∈ A ∧ x ∈ B, intersection_elim 1.
03. 1 ⊢ x ∈ A, conj_eliml 2.
04. ⊢ x ∈ A ∩ B → x ∈ A, subj_intro 3.
05. ⊢ ∀x. x ∈ A ∩ B → x ∈ A, uq_intro 4.
intersection_incl_left. ⊢ A ∩ B ⊆ A, incl_intro 5.

06. 1 ⊢ x ∈ B, conj_elimr 2.
07. ⊢ x ∈ A ∩ B → x ∈ B, subj_intro 6.
08. ⊢ ∀x. x ∈ A ∩ B → x ∈ B, uq_intro 7.
intersection_incl_right. ⊢ A ∩ B ⊆ B, incl_intro 8.

01. 1 ⊢ x ∈ A, hypo.
02. 1 ⊢ x ∈ A ∪ B, union_introl 1.
03. ⊢ x ∈ A → x ∈ A ∪ B, subj_intro 2.
04. ⊢ ∀x. x ∈ A → x ∈ A ∪ B, uq_intro 3.
union_incl_left. ⊢ A ⊆ A ∪ B, incl_intro 4.

01. 1 ⊢ x ∈ B, hypo.
02. 1 ⊢ x ∈ A ∪ B, union_intror 1.
03. ⊢ x ∈ B → x ∈ A ∪ B, subj_intro 2.
04. ⊢ ∀x. x ∈ B → x ∈ A ∪ B, uq_intro 3.
union_incl_right. ⊢ B ⊆ A ∪ B, incl_intro 4.

01. 1 ⊢ x ∈ A ∩ B, hypo.
02. 1 ⊢ x ∈ A, intersection_eliml 1.
03. 1 ⊢ x ∈ B, intersection_elimr 1.
04. 1 ⊢ x ∈ B ∩ A, intersection_intro 3 2.
05. ⊢ x ∈ A ∩ B → x ∈ B ∩ A, subj_intro 4.
06. ⊢ x ∈ B ∩ A → x ∈ A ∩ B, 5.
07. ⊢ x ∈ A ∩ B ↔ x ∈ B ∩ A, bij_intro 5 6.
08. ⊢ ∀x. x ∈ A ∩ B ↔ x ∈ B ∩ A, uq_intro 7.
intersection_comm. ⊢ A ∩ B = B ∩ A, ext 8.

01. 1 ⊢ x ∈ A ∪ B, hypo.
02. 1 ⊢ x ∈ A ∨ x ∈ B, union_elim 1.
03. 3 ⊢ x ∈ A, hypo.
04. 3 ⊢ x ∈ B ∪ A, union_intror 3.
05. 5 ⊢ x ∈ B, hypo.
06. 5 ⊢ x ∈ B ∪ A, union_introl 5.
07. 1 ⊢ x ∈ B ∪ A, disj_elim 2 4 6.
08. ⊢ x ∈ A ∪ B → x ∈ B ∪ A, subj_intro 7.
09. ⊢ x ∈ B ∪ A → x ∈ A ∪ B, 8.
10. ⊢ x ∈ A ∪ B ↔ x ∈ B ∪ A, bij_intro 8 9.
11. ⊢ ∀x. x ∈ A ∪ B ↔ x ∈ B ∪ A, uq_intro 10.
union_comm. ⊢ A ∪ B = B ∪ A, ext 11.

01. 1 ⊢ x ∈ A ∩ (B ∪ C), hypo.
02. 1 ⊢ x ∈ A, intersection_eliml 1.
03. 1 ⊢ x ∈ B ∪ C, intersection_elimr 1.
04. 1 ⊢ x ∈ B ∨ x ∈ C, union_elim 3.
05. 5 ⊢ x ∈ B, hypo.
06. 1, 5 ⊢ x ∈ A ∩ B, intersection_intro 2 5.
07. 1, 5 ⊢ x ∈ (A ∩ B) ∪ (A ∩ C), union_introl 6.
08. 8 ⊢ x ∈ C, hypo.
09. 1, 8 ⊢ x ∈ A ∩ C, intersection_intro 2 8.
10. 1, 8 ⊢ x ∈ (A ∩ B) ∪ (A ∩ C), union_intror 9.
11. 1 ⊢ x ∈ (A ∩ B) ∪ (A ∩ C), disj_elim 4 7 10.
12. ⊢ x ∈ A ∩ (B ∪ C) → x ∈ (A ∩ B) ∪ (A ∩ C), subj_intro 11.

13. 13 ⊢ x ∈ (A ∩ B) ∪ (A ∩ C), hypo.
14. 13 ⊢ x ∈ A ∩ B ∨ x ∈ A ∩ C, union_elim 13.
15. 15 ⊢ x ∈ A ∩ B, hypo.
16. 15 ⊢ x ∈ A, intersection_eliml 15.
17. 15 ⊢ x ∈ B, intersection_elimr 15.
18. 15 ⊢ x ∈ B ∪ C, union_introl 17.
19. 15 ⊢ x ∈ A ∩ (B ∪ C), intersection_intro 16 18.
20. 20 ⊢ x ∈ A ∩ C, hypo.
21. 20 ⊢ x ∈ A, intersection_eliml 20.
22. 20 ⊢ x ∈ C, intersection_elimr 20.
23. 20 ⊢ x ∈ B ∪ C, union_intror 22.
24. 20 ⊢ x ∈ A ∩ (B ∪ C), intersection_intro 21 23.
25. 13 ⊢ x ∈ A ∩ (B ∪ C), disj_elim 14 19 24.
26. ⊢ x ∈ (A ∩ B) ∪ (A ∩ C) → x ∈ A ∩ (B ∪ C), subj_intro 25.

27. ⊢ x ∈ A ∩ (B ∪ C) ↔ x ∈ (A ∩ B) ∪ (A ∩ C), bij_intro 12 26.
28. ⊢ ∀x. x ∈ A ∩ (B ∪ C) ↔ x ∈ (A ∩ B) ∪ (A ∩ C), uq_intro 27.
intersection_distl. ⊢ A ∩ (B ∪ C) = (A ∩ B) ∪ (A ∩ C), ext 28.

01. ⊢ C ∩ (A ∪ B) = (C ∩ A) ∪ (C ∩ B), intersection_distl.
02. ⊢ (A ∪ B) ∩ C = C ∩ (A ∪ B), intersection_comm.
03. ⊢ (A ∪ B) ∩ C = (C ∩ A) ∪ (C ∩ B), eq_trans 2 1.
04. ⊢ C ∩ A = A ∩ C, intersection_comm.
05. ⊢ (A ∪ B) ∩ C = (A ∩ C) ∪ (C ∩ B),
  eq_subst 4 3, P X ↔ (A ∪ B) ∩ C = X ∪ (C ∩ B).
06. ⊢ C ∩ B = B ∩ C, intersection_comm.
intersection_distr. ⊢ (A ∪ B) ∩ C = (A ∩ C) ∪ (B ∩ C),
  eq_subst 6 5, P X ↔ (A ∪ B) ∩ C = (A ∩ C) ∪ X.

01. 1 ⊢ x ∈ A ∪ (B ∩ C), hypo.
02. 1 ⊢ x ∈ A ∨ x ∈ B ∩ C, union_elim 1.
03. 3 ⊢ x ∈ A, hypo.
04. 3 ⊢ x ∈ A ∪ B, union_introl 3.
05. 3 ⊢ x ∈ A ∪ C, union_introl 3.
06. 3 ⊢ x ∈ (A ∪ B) ∩ (A ∪ C), intersection_intro 4 5.
07. 7 ⊢ x ∈ B ∩ C, hypo.
08. 7 ⊢ x ∈ B, intersection_eliml 7.
09. 7 ⊢ x ∈ C, intersection_elimr 7.
10. 7 ⊢ x ∈ A ∪ B, union_intror 8.
11. 7 ⊢ x ∈ A ∪ C, union_intror 9.
12. 7 ⊢ x ∈ (A ∪ B) ∩ (A ∪ C), intersection_intro 10 11.
13. 1 ⊢ x ∈ (A ∪ B) ∩ (A ∪ C), disj_elim 2 6 12.
14. ⊢ x ∈ A ∪ (B ∩ C) → x ∈ (A ∪ B) ∩ (A ∪ C), subj_intro 13.

15. 15 ⊢ x ∈ (A ∪ B) ∩ (A ∪ C), hypo.
16. 15 ⊢ x ∈ A ∪ B, intersection_eliml 15.
17. 15 ⊢ x ∈ A ∪ C, intersection_elimr 15.
18. 15 ⊢ x ∈ A ∨ x ∈ B, union_elim 16.
19. 15 ⊢ x ∈ A ∨ x ∈ C, union_elim 17.
20. 20 ⊢ x ∈ A, hypo.
21. 20 ⊢ x ∈ A ∪ (B ∩ C), union_introl 20.
22. 22 ⊢ x ∈ B, hypo.
23. 23 ⊢ x ∈ C, hypo.
24. 22, 23 ⊢ x ∈ B ∩ C, intersection_intro 22 23.
25. 22, 23 ⊢ x ∈ A ∪ (B ∩ C), union_intror 24.
26. 15, 22 ⊢ x ∈ A ∪ (B ∩ C), disj_elim 19 21 25.
27. 15 ⊢ x ∈ A ∪ (B ∩ C), disj_elim 18 21 26.
28. ⊢ x ∈ (A ∪ B) ∩ (A ∪ C) → x ∈ A ∪ (B ∩ C), subj_intro 27.

29. ⊢ x ∈ A ∪ (B ∩ C) ↔ x ∈ (A ∪ B) ∩ (A ∪ C), bij_intro 14 28.
30. ⊢ ∀x. x ∈ A ∪ (B ∩ C) ↔ x ∈ (A ∪ B) ∩ (A ∪ C), uq_intro 29.
union_distl. ⊢ A ∪ (B ∩ C) = (A ∪ B) ∩ (A ∪ C), ext 30.

01. ⊢ C ∪ (A ∩ B) = (C ∪ A) ∩ (C ∪ B), union_distl.
02. ⊢ (A ∩ B) ∪ C = C ∪ (A ∩ B), union_comm.
03. ⊢ (A ∩ B) ∪ C = (C ∪ A) ∩ (C ∪ B), eq_trans 2 1.
04. ⊢ C ∪ A = A ∪ C, union_comm.
05. ⊢ C ∪ B = B ∪ C, union_comm.
06. ⊢ (A ∩ B) ∪ C = (A ∪ C) ∩ (C ∪ B),
  eq_subst 4 3, P X ↔ (A ∩ B) ∪ C = X ∩ (C ∪ B).
union_distr. ⊢ (A ∩ B) ∪ C = (A ∪ C) ∩ (B ∪ C),
  eq_subst 5 6, P X ↔ (A ∩ B) ∪ C = (A ∪ C) ∩ X.

01. 1 ⊢ x ∈ A, hypo.
02. 2 ⊢ ¬x ∈ B, hypo.
03. 1 ⊢ set x, set_intro 1.
04. 1, 2 ⊢ x ∈ A ∧ ¬x ∈ B, conj_intro 1 2.
05. 1, 2 ⊢ set x ∧ (x ∈ A ∧ ¬x ∈ B), conj_intro 3 4.
06. 1, 2 ⊢ x ∈ {x | x ∈ A ∧ ¬x ∈ B}, comp_intro 5.
07. 1, 2 ⊢ x ∈ A \ B, eq_subst_rev diff_eq 6, P u ↔ x ∈ u.
08. 1 ⊢ ¬x ∈ B → x ∈ A \ B, subj_intro 7.
diff_intro. ⊢ x ∈ A → ¬x ∈ B → x ∈ A \ B, subj_intro 8.

01. 1 ⊢ x ∈ A \ B, hypo.
02. 1 ⊢ x ∈ {x | x ∈ A ∧ ¬x ∈ B}, eq_subst diff_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ set x ∧ (x ∈ A ∧ ¬x ∈ B), comp_elim 2.
04. 1 ⊢ x ∈ A ∧ ¬x ∈ B, conj_elimr 3.
05. 1 ⊢ x ∈ A, conj_eliml 4.
diff_eliml. ⊢ x ∈ A \ B → x ∈ A, subj_intro 5.

07. 1 ⊢ ¬x ∈ B, conj_elimr 4.
diff_elimr. ⊢ x ∈ A \ B → ¬x ∈ B, subj_intro 7.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ ¬x ∈ A, hypo.
03. 1, 2 ⊢ set x ∧ ¬x ∈ A, conj_intro 1 2.
04. 1, 2 ⊢ x ∈ {x | ¬x ∈ A}, comp_intro 3.
05. 1, 2 ⊢ x ∈ compl A, eq_subst_rev compl_eq 4, P u ↔ x ∈ u.
06. 1 ⊢ ¬x ∈ A → x ∈ compl A, subj_intro 5.
compl_intro. ⊢ set x → ¬x ∈ A → x ∈ compl A, subj_intro 6.

01. 1 ⊢ x ∈ compl A, hypo.
02. 1 ⊢ x ∈ {x | ¬x ∈ A}, eq_subst compl_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ set x ∧ ¬x ∈ A, comp_elim 2.
04. 1 ⊢ ¬x ∈ A, conj_elimr 3.
compl_elim. ⊢ x ∈ compl A → ¬x ∈ A, subj_intro 4.

01. 1 ⊢ x ∈ A \ B, hypo.
02. 1 ⊢ x ∈ A, diff_eliml 1.
03. 1 ⊢ ¬x ∈ B, diff_elimr 1.
04. 1 ⊢ set x, set_intro 2.
05. 1 ⊢ set x ∧ ¬x ∈ B, conj_intro 4 3.
06. 1 ⊢ x ∈ {x | ¬x ∈ B}, comp_intro 5.
07. ⊢ compl B = {x | ¬x ∈ B}, compl_eq.
08. 1 ⊢ x ∈ compl B, eq_subst_rev 7 6, P u ↔ x ∈ u.
09. 1 ⊢ x ∈ A ∩ compl B, intersection_intro 2 8.
10. ⊢ x ∈ A \ B → x ∈ A ∩ compl B, subj_intro 9.

11. 11 ⊢ x ∈ A ∩ compl B, hypo.
12. 11 ⊢ x ∈ A, intersection_eliml 11.
13. 11 ⊢ x ∈ compl B, intersection_elimr 11.
14. 11 ⊢ x ∈ {x | ¬x ∈ B}, eq_subst 7 13, P u ↔ x ∈ u.
15. 11 ⊢ set x ∧ ¬x ∈ B, comp_elim 14.
16. 11 ⊢ ¬x ∈ B, conj_elimr 15.
17. 11 ⊢ x ∈ A \ B, diff_intro 12 16.
18. ⊢ x ∈ A ∩ compl B → x ∈ A \ B, subj_intro 17.

19. ⊢ x ∈ A \ B ↔ x ∈ A ∩ compl B, bij_intro 10 18.
20. ⊢ ∀x. x ∈ A \ B ↔ x ∈ A ∩ compl B, uq_intro 19.
diff_as_inter. ⊢ A \ B = A ∩ compl B, ext 20.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ ∀y. y ∈ M → x ∈ y, hypo.
03. 1, 2 ⊢ set x ∧ ∀y. y ∈ M → x ∈ y, conj_intro 1 2.
04. 1, 2 ⊢ x ∈ {x | ∀y. y ∈ M → x ∈ y}, comp_intro 3.
05. 1, 2 ⊢ x ∈ ⋂M, eq_subst_rev Intersection_eq 4, P A ↔ x ∈ A.
06. 1 ⊢ (∀y. y ∈ M → x ∈ y) → x ∈ ⋂M, subj_intro 5.
Intersection_intro. ⊢ set x → (∀y. y ∈ M → x ∈ y) → x ∈ ⋂M,
  subj_intro 6.

01. 1 ⊢ x ∈ ⋂M, hypo.
02. 1 ⊢ x ∈ {x | ∀y. y ∈ M → x ∈ y},
  eq_subst Intersection_eq 1, P A ↔ x ∈ A.
03. 1 ⊢ set x ∧ ∀y. y ∈ M → x ∈ y, comp_elim 2.
04. 1 ⊢ ∀y. y ∈ M → x ∈ y, conj_elimr 3.
05. 1 ⊢ y ∈ M → x ∈ y, uq_elim 4.
Intersection_elim. ⊢ x ∈ ⋂M → y ∈ M → x ∈ y, subj_intro 5.

01. 1 ⊢ ∃y. y ∈ M ∧ x ∈ y, hypo.
02. 2 ⊢ y ∈ M ∧ x ∈ y, hypo.
03. 2 ⊢ x ∈ y, conj_elimr 2.
04. 2 ⊢ set x, set_intro 3.
05. 1 ⊢ set x, ex_elim 1 4.
06. 1 ⊢ set x ∧ ∃y. y ∈ M ∧ x ∈ y, conj_intro 5 1.
07. 1 ⊢ x ∈ {x | ∃y. y ∈ M ∧ x ∈ y}, comp_intro 6.
08. 1 ⊢ x ∈ ⋃M, eq_subst_rev Union_eq 7, P u ↔ x ∈ u.
Union_intro. ⊢ (∃y. y ∈ M ∧ x ∈ y) → x ∈ ⋃M, subj_intro 8.

01. 1 ⊢ x ∈ ⋃M, hypo.
02. 1 ⊢ x ∈ {x | ∃y. y ∈ M ∧ x ∈ y}, eq_subst Union_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ set x ∧ ∃y. y ∈ M ∧ x ∈ y, comp_elim 2.
04. 1 ⊢ ∃y. y ∈ M ∧ x ∈ y, conj_elimr 3.
Union_elim. ⊢ x ∈ ⋃M → ∃y. y ∈ M ∧ x ∈ y, subj_intro 4.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ ⋂B, hypo.
03. 3 ⊢ y ∈ A, hypo.
04. 1, 3 ⊢ y ∈ B, incl_elim 1 3.
05. 1, 2, 3 ⊢ x ∈ y, Intersection_elim 2 4.
06. 1, 2 ⊢ y ∈ A → x ∈ y, subj_intro 5.
07. 1, 2 ⊢ ∀y. y ∈ A → x ∈ y, uq_intro 6.
08. 2 ⊢ set x, set_intro 2.
09. 1, 2 ⊢ x ∈ ⋂A, Intersection_intro 8 7.
10. 1 ⊢ x ∈ ⋂B → x ∈ ⋂A, subj_intro 9.
11. 1 ⊢ ∀x. x ∈ ⋂B → x ∈ ⋂A, uq_intro 10.
12. 1 ⊢ ⋂B ⊆ ⋂A, incl_intro 11.
Intersection_dec. ⊢ A ⊆ B → ⋂B ⊆ ⋂A, subj_intro 12.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ ⋃A, hypo.
03. 2 ⊢ ∃y. y ∈ A ∧ x ∈ y, Union_elim 2.
04. 4 ⊢ y ∈ A ∧ x ∈ y, hypo.
05. 4 ⊢ y ∈ A, conj_eliml 4.
06. 4 ⊢ x ∈ y, conj_elimr 4.
07. 1, 4 ⊢ y ∈ B, incl_elim 1 5.
08. 1, 4 ⊢ y ∈ B ∧ x ∈ y, conj_intro 7 6.
09. 1, 4 ⊢ ∃y. y ∈ B ∧ x ∈ y, ex_intro 8.
10. 1, 4 ⊢ x ∈ ⋃B, Union_intro 9.
11. 1, 2 ⊢ x ∈ ⋃B, ex_elim 3 10.
12. 1 ⊢ x ∈ ⋃A → x ∈ ⋃B, subj_intro 11.
13. 1 ⊢ ∀x. x ∈ ⋃A → x ∈ ⋃B, uq_intro 12.
14. 1 ⊢ ⋃A ⊆ ⋃B, incl_intro 13.
Union_inc. ⊢ A ⊆ B → ⋃A ⊆ ⋃B, subj_intro 14.

# The universal class and Russell's paradox.

01. 1 ⊢ set x, hypo.
02. ⊢ x = x, eq_refl.
03. 1 ⊢ set x ∧ x = x, conj_intro 1 2.
04. 1 ⊢ x ∈ {x | x = x}, comp_intro 3.
05. 1 ⊢ x ∈ UnivCl, eq_subst_rev UnivCl_eq 4, P A ↔ x ∈ A.
UnivCl_intro. ⊢ set x → x ∈ UnivCl, subj_intro 5.

01. 1 ⊢ x ∈ UnivCl, hypo.
02. 1 ⊢ set x, set_intro 1.
UnivCl_elim. ⊢ x ∈ UnivCl → set x, subj_intro 2.

01. 1 ⊢ x ∈ A, hypo.
02. 1 ⊢ set x, set_intro 1.
03. 1 ⊢ x ∈ UnivCl, UnivCl_intro 2.
04. ⊢ x ∈ A → x ∈ UnivCl, subj_intro 3.
05. ⊢ ∀x. x ∈ A → x ∈ UnivCl, uq_intro 4.
incl_UnivCl. ⊢ A ⊆ UnivCl, incl_intro 5.

01. 1 ⊢ R = {x | ¬x ∈ x}, hypo.
02. 2 ⊢ u ∈ R, hypo.
03. 1, 2 ⊢ u ∈ {x | ¬x ∈ x}, eq_subst 1 2.
04. 1, 2 ⊢ set u ∧ ¬u ∈ u, comp_elim 3.
05. 1, 2 ⊢ ¬u ∈ u, conj_elimr 4.
06. 1 ⊢ u ∈ R → ¬u ∈ u, subj_intro 5.
07. 1 ⊢ R ∈ R → ¬R ∈ R, 6.

08. 8 ⊢ set R, hypo.
09. 9 ⊢ ¬R ∈ R, hypo.
10. 8, 9 ⊢ set R ∧ ¬R ∈ R, conj_intro 8 9.
11. 8, 9 ⊢ R ∈ {x | ¬x ∈ x}, comp_intro 10.
12. 1, 8, 9 ⊢ R ∈ R, eq_subst_rev 1 11, P x ↔ R ∈ x.
13. 1, 8 ⊢ ¬R ∈ R → R ∈ R, subj_intro 12.

14. 1, 8 ⊢ R ∈ R ↔ ¬R ∈ R, bij_intro 7 13.
15. ⊢ ¬(R ∈ R ↔ ¬R ∈ R), diag_contra.
16. 1, 8 ⊢ ⊥, neg_elim 15 14.
17. 1 ⊢ ¬set R, neg_intro 16.
18. ⊢ R = {x | ¬x ∈ x} → ¬set R, subj_intro 17.

19. ⊢ DiagCl = {x | ¬x ∈ x} → ¬set DiagCl, 18.
DiagCl_is_proper. ⊢ ¬set DiagCl, subj_elim 19 DiagCl_eq.

01. 1 ⊢ x ∈ DiagCl, hypo.
02. 1 ⊢ set x, set_intro 1.
03. 1 ⊢ x ∈ UnivCl, UnivCl_intro 2.
04. ⊢ x ∈ DiagCl → x ∈ UnivCl, subj_intro 3.
05. ⊢ ∀x. x ∈ DiagCl → x ∈ UnivCl, uq_intro 4.
06. ⊢ DiagCl ⊆ UnivCl, incl_intro 5.
07. ⊢ DiagCl ⊆ UnivCl → set UnivCl → set DiagCl, subset.
08. ⊢ set UnivCl → set DiagCl, subj_elim 7 6.
09. 9 ⊢ set UnivCl, hypo.
10. 9 ⊢ set DiagCl, subj_elim 8 9.
11. 9 ⊢ ⊥, neg_elim DiagCl_is_proper 10.
UnivCl_is_proper. ⊢ ¬set UnivCl, neg_intro 11.

01. 1 ⊢ x ∈ ∅, hypo.
02. 1 ⊢ x ∈ {x | ⊥}, eq_subst empty_set_eq 1.
03. 1 ⊢ set x ∧ ⊥, comp_elim 2.
04. 1 ⊢ ⊥, conj_elimr 3.
05. 1 ⊢ A, efq 4.
empty_set_efq. ⊢ x ∈ ∅ → A, subj_intro 5.

01. 1 ⊢ x ∈ ∅, hypo.
02. 1 ⊢ x ∈ A, empty_set_efq 1.
03. ⊢ x ∈ ∅ → x ∈ A, subj_intro 2.
04. ⊢ ∀x. x ∈ ∅ → x ∈ A, uq_intro 3.
empty_set_incl. ⊢ ∅ ⊆ A, incl_intro 4.

01. 1 ⊢ x ∈ ∅ ∪ A, hypo.
02. 1 ⊢ x ∈ ∅ ∨ x ∈ A, union_elim 1.
03. 3 ⊢ x ∈ ∅, hypo.
04. 3 ⊢ x ∈ A, empty_set_efq 3.
05. 5 ⊢ x ∈ A, hypo.
06. 1 ⊢ x ∈ A, disj_elim 2 4 5.
07. ⊢ x ∈ ∅ ∪ A → x ∈ A, subj_intro 6.
08. 8 ⊢ x ∈ A, hypo.
09. 8 ⊢ x ∈ ∅ ∪ A, union_intror 8.
10. ⊢ x ∈ A → x ∈ ∅ ∪ A, subj_intro 9.
11. ⊢ x ∈ ∅ ∪ A ↔ x ∈ A, bij_intro 7 10.
12. ⊢ ∀x. x ∈ ∅ ∪ A ↔ x ∈ A, uq_intro 11.
union_neutl. ⊢ ∅ ∪ A = A, ext 12.

01. ⊢ A ∪ ∅ = ∅ ∪ A, union_comm.
union_neutr. ⊢ A ∪ ∅ = A, eq_trans 1 union_neutl.

01. 1 ⊢ x ∈ A ∩ UnivCl, hypo.
02. 1 ⊢ x ∈ A, intersection_eliml 1.
03. ⊢ x ∈ A ∩ UnivCl → x ∈ A, subj_intro 2.
04. 4 ⊢ x ∈ A, hypo.
05. 4 ⊢ set x, set_intro 4.
06. 4 ⊢ x ∈ UnivCl, UnivCl_intro 5.
07. 4 ⊢ x ∈ A ∩ UnivCl, intersection_intro 4 6.
08. ⊢ x ∈ A → x ∈ A ∩ UnivCl, subj_intro 7.
09. ⊢ x ∈ A ∩ UnivCl ↔ x ∈ A, bij_intro 3 8.
10. ⊢ ∀x. x ∈ A ∩ UnivCl ↔ x ∈ A, uq_intro 9.
intersection_neutr. ⊢ A ∩ UnivCl = A, ext 10.

01. ⊢ UnivCl ∩ A = A ∩ UnivCl, intersection_comm.
intersection_neutl. ⊢ UnivCl ∩ A = A, eq_trans 1 intersection_neutr.

01. 1 ⊢ x ∈ A ∩ compl A, hypo.
02. 1 ⊢ x ∈ A, intersection_eliml 1.
03. 1 ⊢ x ∈ compl A, intersection_elimr 1.
04. 1 ⊢ ¬x ∈ A, compl_elim 3.
05. 1 ⊢ ⊥, neg_elim 4 2.
06. 1 ⊢ x ∈ ∅, efq 5.
07. ⊢ x ∈ A ∩ compl A → x ∈ ∅, subj_intro 6.
08. ⊢ ∀x. x ∈ A ∩ compl A → x ∈ ∅, uq_intro 7.
09. ⊢ A ∩ compl A ⊆ ∅, incl_intro 8.
10. ⊢ ∅ ⊆ A ∩ compl A, empty_set_incl.
intersection_compl. ⊢ A ∩ compl A = ∅, incl_antisym 9 10.

01. 1 ⊢ x ∈ UnivCl, hypo.
02. 1 ⊢ set x, set_intro 1.
03. ⊢ x ∈ A ∨ ¬x ∈ A, lem.
04. 4 ⊢ x ∈ A, hypo.
05. 4 ⊢ x ∈ A ∪ compl A, union_introl 4.
06. 6 ⊢ ¬x ∈ A, hypo.
07. 1, 6 ⊢ x ∈ compl A, compl_intro 2 6.
08. 1, 6 ⊢ x ∈ A ∪ compl A, union_intror 7.
09. 1 ⊢ x ∈ A ∪ compl A, disj_elim 3 5 8.
10. ⊢ x ∈ UnivCl → x ∈ A ∪ compl A, subj_intro 9.
11. ⊢ ∀x. x ∈ UnivCl → x ∈ A ∪ compl A, uq_intro 10.
12. ⊢ UnivCl ⊆ A ∪ compl A, incl_intro 11.
13. ⊢ A ∪ compl A ⊆ UnivCl, incl_UnivCl.
union_compl. ⊢ A ∪ compl A = UnivCl, incl_antisym 13 12.

# Singletons, pair sets and pairs

01. 1 ⊢ set a, hypo.
02. 2 ⊢ x ∈ {a}, hypo.
03. 2 ⊢ x ∈ {x | set a → x = a}, eq_subst sg_eq 2, P u ↔ x ∈ u.
04. 2 ⊢ set x ∧ (set a → x = a), comp_elim 3.
05. 2 ⊢ set a → x = a, conj_elimr 4.
06. 1, 2 ⊢ x = a, subj_elim 5 1.
07. 1 ⊢ x ∈ {a} → x = a, subj_intro 6.
sg_elim. ⊢ set a → x ∈ {a} → x = a, subj_intro 7.

09. 9 ⊢ set a, hypo.
10. 10 ⊢ x = a, hypo.
11. 9, 10 ⊢ set x, eq_subst_rev 10 9, P u ↔ set u.
12. 10, 9 ⊢ x = a, wk 10.
13. 10 ⊢ set a → x = a, subj_intro 12.
14. 9, 10 ⊢ set x ∧ (set a → x = a), conj_intro 11 13.
15. 9, 10 ⊢ x ∈ {x | set a → x = a}, comp_intro 14.
16. 9, 10 ⊢ x ∈ {a}, eq_subst_rev sg_eq 15, P u ↔ x ∈ u.
17. 9 ⊢ x = a → x ∈ {a}, subj_intro 16.
sg_intro. ⊢ set a → x = a → x ∈ {a}, subj_intro 17.

19. 1 ⊢ x ∈ {a} ↔ x = a, bij_intro 7 17.
sg_equi. ⊢ set a → (x ∈ {a} ↔ x = a), subj_intro 19.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ x ∈ ⋂{a}, hypo.
03. ⊢ a = a, eq_refl.
04. 1 ⊢ a ∈ {a}, sg_intro 1 3.
05. 1, 2 ⊢ x ∈ a, Intersection_elim 2 4.
06. 1 ⊢ x ∈ ⋂{a} → x ∈ a, subj_intro 5.
07. 1 ⊢ ∀x. x ∈ ⋂{a} → x ∈ a, uq_intro 6.
08. 1 ⊢ ⋂{a} ⊆ a, incl_intro 7.

09. 9 ⊢ x ∈ a, hypo.
10. 10 ⊢ y ∈ {a}, hypo.
11. 1, 10 ⊢ y = a, sg_elim 1 10.
12. 1, 9, 10 ⊢ x ∈ y, eq_subst_rev 11 9, P u ↔ x ∈ u.
13. 1, 9 ⊢ y ∈ {a} → x ∈ y, subj_intro 12.
14. 1, 9 ⊢ ∀y. y ∈ {a} → x ∈ y, uq_intro 13.
15. 9 ⊢ set x, set_intro 9.
16. 1, 9 ⊢ x ∈ ⋂{a}, Intersection_intro 15 14.
17. 1 ⊢ x ∈ a → x ∈ ⋂{a}, subj_intro 16.
18. 1 ⊢ ∀x. x ∈ a → x ∈ ⋂{a}, uq_intro 17.
19. 1 ⊢ a ⊆ ⋂{a}, incl_intro 18.

20. 1 ⊢ ⋂{a} = a, incl_antisym 8 19.
Intersection_sg. ⊢ set a → ⋂{a} = a, subj_intro 20.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. 4 ⊢ {x} = {y}, hypo.
05. 4 ⊢ ⋂{x} = ⋂{y}, f_equal 4.
06. 1 ⊢ ⋂{x} = x, Intersection_sg 2.
07. 1 ⊢ ⋂{y} = y, Intersection_sg 3.
08. 1, 4 ⊢ x = ⋂{y}, eq_subst 6 5, P u ↔ u = ⋂{y}.
09. 1, 4 ⊢ x = y, eq_subst 7 8, P u ↔ x = u.
10. 1 ⊢ {x} = {y} → x = y, subj_intro 9.
sg_inj. ⊢ set x ∧ set y → {x} = {y} → x = y, subj_intro 10.

01. 1 ⊢ ¬set A, hypo.
02. 2 ⊢ set A, hypo.
03. 1, 2 ⊢ ⊥, neg_elim 1 2.
04. 1, 2 ⊢ x = A, efq 3.
05. 1 ⊢ set A → x = A, subj_intro 4.
06. 6 ⊢ x ∈ UnivCl, hypo.
07. 6 ⊢ set x, set_intro 6.
08. 1, 6 ⊢ set x ∧ (set A → x = A), conj_intro 7 5.
09. 1, 6 ⊢ x ∈ {x | set A → x = A}, comp_intro 8.
10. ⊢ {A} = {x | set A → x = A}, sg_eq.
11. 1, 6 ⊢ x ∈ {A}, eq_subst_rev 10 9, P u ↔ x ∈ u.
12. 1 ⊢ x ∈ UnivCl → x ∈ {A}, subj_intro 11.
13. 1 ⊢ ∀x. x ∈ UnivCl → x ∈ {A}, uq_intro 12.
14. 1 ⊢ UnivCl ⊆ {A}, incl_intro 13.
15. ⊢ {A} ⊆ UnivCl, incl_UnivCl.
16. 1 ⊢ {A} = UnivCl, incl_antisym 15 14.
sg_of_proper_class. ⊢ ¬set A → {A} = UnivCl, subj_intro 16.

01. 1 ⊢ set x, hypo.
02. ⊢ x = x, eq_refl.
03. 1 ⊢ x ∈ {x}, sg_intro 1 2.
04. ⊢ {x} ⊆ {x, y}, union_incl_left.
05. 1 ⊢ x ∈ {x, y}, incl_elim 4 3.
in21. ⊢ set x → x ∈ {x, y}, subj_intro 5.

01. 1 ⊢ set y, hypo.
02. ⊢ y = y, eq_refl.
03. 1 ⊢ y ∈ {y}, sg_intro 1 2.
04. ⊢ {y} ⊆ {x, y}, union_incl_right.
05. 1 ⊢ y ∈ {x, y}, incl_elim 4 3.
in22. ⊢ set y → y ∈ {x, y}, subj_intro 5.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. 1 ⊢ x ∈ {x, y}, in21 2.
05. 1 ⊢ y ∈ {x, y}, in22 3.

06. 6 ⊢ u ∈ ⋂{x, y}, hypo.
07. 1, 6 ⊢ u ∈ x, Intersection_elim 6 4.
08. 1, 6 ⊢ u ∈ y, Intersection_elim 6 5.
09. 1, 6 ⊢ u ∈ x ∩ y, intersection_intro 7 8.
10. 1 ⊢ u ∈ ⋂{x, y} → u ∈ x ∩ y, subj_intro 9.
11. 1 ⊢ ∀u. u ∈ ⋂{x, y} → u ∈ x ∩ y, uq_intro 10.
12. 1 ⊢ ⋂{x, y} ⊆ x ∩ y, incl_intro 11.

13. 13 ⊢ u ∈ x ∩ y, hypo.
14. 13 ⊢ u ∈ x, intersection_eliml 13.
15. 13 ⊢ u ∈ y, intersection_elimr 13.
16. 13 ⊢ set u, set_intro 13.
17. 17 ⊢ a ∈ {x, y}, hypo.
18. 17 ⊢ a ∈ {x} ∨ a ∈ {y}, union_elim 17.
19. 19 ⊢ a ∈ {x}, hypo.
20. 1, 19 ⊢ a = x, sg_elim 2 19.
21. 1, 13, 19 ⊢ u ∈ a, eq_subst_rev 20 14, P x ↔ u ∈ x.
22. 22 ⊢ a ∈ {y}, hypo.
23. 1, 22 ⊢ a = y, sg_elim 3 22.
24. 1, 13, 22 ⊢ u ∈ a, eq_subst_rev 23 15, P x ↔ u ∈ x.
25. 1, 13, 17 ⊢ u ∈ a, disj_elim 18 21 24.
26. 1, 13 ⊢ a ∈ {x, y} → u ∈ a, subj_intro 25.
27. 1, 13 ⊢ ∀a. a ∈ {x, y} → u ∈ a, uq_intro 26.
28. 1, 13 ⊢ u ∈ ⋂{x, y}, Intersection_intro 16 27.
29. 1 ⊢ u ∈ x ∩ y → u ∈ ⋂{x, y}, subj_intro 28.
30. 1 ⊢ ∀u. u ∈ x ∩ y → u ∈ ⋂{x, y}, uq_intro 29.
31. 1 ⊢ x ∩ y ⊆ ⋂{x, y}, incl_intro 30.

32. 1 ⊢ ⋂{x, y} = x ∩ y, incl_antisym 12 31.
Intersection_pair_set. ⊢ set x ∧ set y → ⋂{x, y} = x ∩ y,
  subj_intro 32.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.

04. 4 ⊢ u ∈ ⋃{x, y}, hypo.
05. 4 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, Union_elim 4.
06. 6 ⊢ a ∈ {x, y} ∧ u ∈ a, hypo.
07. 6 ⊢ a ∈ {x, y}, conj_eliml 6.
08. 6 ⊢ u ∈ a, conj_elimr 6.
09. 6 ⊢ a ∈ {x} ∨ a ∈ {y}, union_elim 7.

10. 10 ⊢ a ∈ {x}, hypo.
11. 1, 10 ⊢ a = x, sg_elim 2 10.
12. 1, 6, 10 ⊢ u ∈ x, eq_subst 11 08, P x ↔ u ∈ x.
13. 1, 6, 10 ⊢ u ∈ x ∪ y, union_introl 12.

14. 14 ⊢ a ∈ {y}, hypo.
15. 1, 14 ⊢ a = y, sg_elim 3 14.
16. 1, 6, 14 ⊢ u ∈ y, eq_subst 15 08, P x ↔ u ∈ x.
17. 1, 6, 14 ⊢ u ∈ x ∪ y, union_intror 16.

18. 1, 6 ⊢ u ∈ x ∪ y, disj_elim 9 13 17.
19. 1, 4 ⊢ u ∈ x ∪ y, ex_elim 5 18.
20. 1 ⊢ u ∈ ⋃{x, y} → u ∈ x ∪ y, subj_intro 19.
21. 1 ⊢ ∀u. u ∈ ⋃{x, y} → u ∈ x ∪ y, uq_intro 20.
22. 1 ⊢ ⋃{x, y} ⊆ x ∪ y, incl_intro 21.

23. 23 ⊢ u ∈ x ∪ y, hypo.
24. 23 ⊢ u ∈ x ∨ u ∈ y, union_elim 23.

25. 25 ⊢ u ∈ x, hypo.
26. 1 ⊢ x ∈ {x, y}, in21 2.
27. 1, 25 ⊢ x ∈ {x, y} ∧ u ∈ x, conj_intro 26 25.
28. 1, 25 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, ex_intro 27.
29. 1, 25 ⊢ u ∈ ⋃{x, y}, Union_intro 28.

30. 30 ⊢ u ∈ y, hypo.
31. 1 ⊢ y ∈ {x, y}, in22 3.
32. 1, 30 ⊢ y ∈ {x, y} ∧ u ∈ y, conj_intro 31 30.
33. 1, 30 ⊢ ∃a. a ∈ {x, y} ∧ u ∈ a, ex_intro 32.
34. 1, 30 ⊢ u ∈ ⋃{x, y}, Union_intro 33.

35. 1, 23 ⊢ u ∈ ⋃{x, y}, disj_elim 24 29 34.
36. 1 ⊢ u ∈ x ∪ y → u ∈ ⋃{x, y}, subj_intro 35.
37. 1 ⊢ ∀u. u ∈ x ∪ y → u ∈ ⋃{x, y}, uq_intro 36.
38. 1 ⊢ x ∪ y ⊆ ⋃{x, y}, incl_intro 37.

39. 1 ⊢ ⋃{x, y} = x ∪ y, incl_antisym 22 38.
Union_pair_set. ⊢ set x ∧ set y → ⋃{x, y} = x ∪ y, subj_intro 39.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ A, hypo.
03. 1, 2 ⊢ x ∈ B, incl_elim 1 2.
04. 1, 2 ⊢ x ∈ A ∩ B, intersection_intro 2 3.
05. 1 ⊢ x ∈ A → x ∈ A ∩ B, subj_intro 4.
06. 1 ⊢ ∀x. x ∈ A → x ∈ A ∩ B, uq_intro 5.
07. 1 ⊢ A ⊆ A ∩ B, incl_intro 6.
08. ⊢ A ∩ B ⊆ A, intersection_incl_left.
09. 1 ⊢ A ∩ B = A, incl_antisym 8 7.
intersection_from_incl. ⊢ A ⊆ B → A ∩ B = A, subj_intro 9.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ x ∈ A ∪ B, hypo.
03. 2 ⊢ x ∈ A ∨ x ∈ B, union_elim 2.
04. 4 ⊢ x ∈ A, hypo.
05. 1, 4 ⊢ x ∈ B, incl_elim 1 4.
06. 6 ⊢ x ∈ B, hypo.
07. 1, 2 ⊢ x ∈ B, disj_elim 3 5 6.
08. 1 ⊢ x ∈ A ∪ B → x ∈ B, subj_intro 7.
09. 1 ⊢ ∀x. x ∈ A ∪ B → x ∈ B, uq_intro 8.
10. 1 ⊢ A ∪ B ⊆ B, incl_intro 9.
11. ⊢ B ⊆ A ∪ B, union_incl_right.
12. 1 ⊢ A ∪ B = B, incl_antisym 10 11.
union_from_incl. ⊢ A ⊆ B → A ∪ B = B, subj_intro 12.

01. ⊢ A ⊆ A, incl_refl.
intersection_idem. ⊢ A ∩ A = A, intersection_from_incl 1.

01. ⊢ A ⊆ A, incl_refl.
union_idem. ⊢ A ∪ A = A, union_from_incl 1.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ set y, hypo.
03. 1, 2 ⊢ set {x, y}, pairing 1 2.
04. ⊢ set {x, y} → set (⋃{x, y}), union.
05. 1, 2 ⊢ set (⋃{x, y}), subj_elim 4 3.
06. 1, 2 ⊢ set x ∧ set y, conj_intro 1 2.
07. 1, 2 ⊢ ⋃{x, y} = x ∪ y, Union_pair_set 6.
08. 1, 2 ⊢ set (x ∪ y), eq_subst 7 5, P u ↔ set u.
09. 1 ⊢ set y → set (x ∪ y), subj_intro 8.
set_union. ⊢ set x → set y → set (x ∪ y), subj_intro 9.

01. 1 ⊢ set x, hypo.
02. 1 ⊢ set {x, x}, pairing 1 1.
03. ⊢ {x, x} = {x}, union_idem.
04. 1 ⊢ set {x}, eq_subst 3 2, P u ↔ set u.
set_sg. ⊢ set x → set {x}, subj_intro 4.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ set y, hypo.
03. 1 ⊢ set {x}, set_sg 1.
04. 1 ⊢ set {{x}}, set_sg 3.
05. 1, 2 ⊢ set {x, y}, pairing 1 2.
06. 1, 2 ⊢ set {{x, y}}, set_sg 5.
07. 1, 2 ⊢ set {{x}, {x, y}}, set_union 4 6.
08. 1, 2 ⊢ set (x, y), eq_subst_rev pair_eq 7.
09. 1 ⊢ set y → set (x, y), subj_intro 8.
set_pair. ⊢ set x → set y → set (x, y), subj_intro 9.

01. 1 ⊢ ¬set x, hypo.
02. 1 ⊢ {x} = UnivCl, sg_of_proper_class 1.
03. 1 ⊢ ¬set {x}, eq_subst_rev 2 UnivCl_is_proper, P u ↔ ¬set u.
04. ⊢ ¬set x → ¬set {x}, subj_intro 3.
set_from_sg. ⊢ set {x} → set x, contraposition_rev 4.

01. ⊢ A ⊆ A ∪ B, union_incl_left.
02. ⊢ A ⊆ A ∪ B → set (A ∪ B) → set A, subset.
setl_from_union. ⊢ set (A ∪ B) → set A, subj_elim 2 1.

01. ⊢ B ⊆ A ∪ B, union_incl_right.
02. ⊢ B ⊆ A ∪ B → set (A ∪ B) → set B, subset.
setr_from_union. ⊢ set (A ∪ B) → set B, subj_elim 2 1.

01. 1 ⊢ set (x, y), hypo.
02. ⊢ (x, y) = {{x}, {x, y}}, pair_eq.
03. 1 ⊢ set {{x}, {x, y}}, eq_subst 2 1, P u ↔ set u.
04. 1 ⊢ set {{x}}, setl_from_union 3.
05. 1 ⊢ set {x}, set_from_sg 4.
06. 1 ⊢ set x, set_from_sg 5.
setl_from_pair. ⊢ set (x, y) → set x, subj_intro 6.

08. 1 ⊢ set {{x, y}}, setr_from_union 3.
09. 1 ⊢ set {x, y}, set_from_sg 8.
10. 1 ⊢ set {y}, setr_from_union 9.
11. 1 ⊢ set y, set_from_sg 10.
setr_from_pair. ⊢ set (x, y) → set y, subj_intro 11.

01. 1 ⊢ set x ∧ set y, hypo.
02. 1 ⊢ set x, conj_eliml 1.
03. 1 ⊢ set y, conj_elimr 1.
04. ⊢ ⋂(x, y) = ⋂(x, y), eq_refl.
05. ⊢ (x, y) = {{x}, {x, y}}, pair_eq.
06. ⊢ ⋂(x, y) = ⋂{{x}, {x, y}}, eq_subst 5 4, P u ↔ ⋂(x, y) = ⋂u.
07. 1 ⊢ set {x}, set_sg 2.
08. 1 ⊢ set {x, y}, pairing 2 3.
09. 1 ⊢ set {x} ∧ set {x, y}, conj_intro 7 8.
10. 1 ⊢ ⋂{{x}, {x, y}} = {x} ∩ {x, y}, Intersection_pair_set 9.
11. 1 ⊢ ⋂(x, y) = {x} ∩ {x, y}, eq_trans 6 10.
12. ⊢ {x} ⊆ {x, y}, union_incl_left.
13. ⊢ {x} ∩ {x, y} = {x}, intersection_from_incl 12.
14. 1 ⊢ ⋂(x, y) = {x}, eq_trans 11 13.
15. 1 ⊢ ⋂⋂(x, y) = ⋂{x}, f_equal 14.
16. 1 ⊢ ⋂{x} = x, Intersection_sg 2.
17. 1 ⊢ ⋂⋂(x, y) = x, eq_trans 15 16.
18. ⊢ fst (x, y) = ⋂⋂(x, y), fst_eq.
19. 1 ⊢ fst (x, y) = x, eq_trans 18 17.
pair_fst_conj. ⊢ set x ∧ set y → fst (x, y) = x, subj_intro 19.

21. ⊢ ⋃(x, y) = ⋃(x, y), eq_refl.
22. ⊢ ⋃(x, y) = ⋃{{x}, {x, y}}, eq_subst 5 21, P u ↔ ⋃(x, y) = ⋃u.
23. 1 ⊢ ⋃{{x}, {x, y}} = {x} ∪ {x, y}, Union_pair_set 9.
24. 1 ⊢ ⋃(x, y) = {x} ∪ {x, y}, eq_trans 22 23.
25. ⊢ {x} ∪ {x, y} = {x, y}, union_from_incl 12.
26. 1 ⊢ ⋃(x, y) = {x, y}, eq_trans 24 25.
27. 1 ⊢ ⋃⋃(x, y) = ⋃{x, y}, f_equal 26.
28. 1 ⊢ ⋃{x, y} = x ∪ y, Union_pair_set 1.
29. 1 ⊢ ⋃⋃(x, y) = x ∪ y, eq_trans 27 28.

30. 1 ⊢ ⋂{x, y} = x ∩ y, Intersection_pair_set 1.
31. 1 ⊢ ⋂⋃(x, y) = ⋂{x, y}, f_equal 26.
32. 1 ⊢ ⋂⋃(x, y) = x ∩ y, eq_trans 31 30.

33. ⊢ scd (x, y) = ⋂⋃(x, y) ∪ (⋃⋃(x, y) \ ⋂⋂(x, y)), scd_eq.
34. 1 ⊢ scd (x, y) = (x ∩ y) ∪ (⋃⋃(x, y) \ ⋂⋂(x, y)),
  eq_subst 32 33, P a ↔ scd (x, y) = a ∪ (⋃⋃(x, y) \ ⋂⋂(x, y)).
35. 1 ⊢ scd (x, y) = (x ∩ y) ∪ ((x ∪ y) \ ⋂⋂(x, y)),
  eq_subst 29 34, P a ↔ scd (x, y) = (x ∩ y) ∪ (a \ ⋂⋂(x, y)).
36. 1 ⊢ scd (x, y) = (x ∩ y) ∪ ((x ∪ y) \ x),
  eq_subst 17 35, P a ↔ scd (x, y) = (x ∩ y) ∪ ((x ∪ y) \ a).
37. ⊢ (x ∪ y) \ x = (x ∪ y) ∩ compl x, diff_as_inter.
38. 1 ⊢ scd (x, y) = (x ∩ y) ∪ ((x ∪ y) ∩ compl x),
  eq_subst 37 36, P a ↔ scd (x, y) = (x ∩ y) ∪ a.
39. ⊢ (x ∪ y) ∩ compl x = (x ∩ compl x) ∪ (y ∩ compl x),
  intersection_distr.
40. ⊢ x ∩ compl x = ∅, intersection_compl.
41. ⊢ (x ∩ compl x) ∪ (y ∩ compl x) = ∅ ∪ (y ∩ compl x),
  f_equal 40, f a = a ∪ (y ∩ compl x).
42. ⊢ ∅ ∪ (y ∩ compl x) = y ∩ compl x, union_neutl.
43. ⊢ (x ∩ compl x) ∪ (y ∩ compl x) = y ∩ compl x, eq_trans 41 42.
44. ⊢ (x ∪ y) ∩ compl x = y ∩ compl x, eq_trans 39 43.
45. ⊢ y ∩ compl x = compl x ∩ y, intersection_comm.
46. ⊢ (x ∪ y) ∩ compl x = compl x ∩ y, eq_trans 44 45.
47. 1 ⊢ scd (x, y) = (x ∩ y) ∪ (compl x ∩ y),
  eq_subst 46 38, P a ↔ scd (x, y) = (x ∩ y) ∪ a.
48. ⊢ (x ∪ compl x) ∩ y = (x ∩ y) ∪ (compl x ∩ y), intersection_distr.
49. ⊢ (x ∩ y) ∪ (compl x ∩ y) = (x ∪ compl x) ∩ y, eq_symm 48.
50. 1 ⊢ scd (x, y) = (x ∪ compl x) ∩ y, eq_trans 47 49.
51. ⊢ x ∪ compl x = UnivCl, union_compl.
52. ⊢ (x ∪ compl x) ∩ y = UnivCl ∩ y, f_equal 51, f a = a ∩ y.
53. ⊢ UnivCl ∩ y = y, intersection_neutl.
54. ⊢ (x ∪ compl x) ∩ y = y, eq_trans 52 53.
55. 1 ⊢ scd (x, y) = y, eq_trans 50 54.
pair_scd_conj. ⊢ set x ∧ set y → scd (x, y) = y, subj_intro 55.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ set y, hypo.
03. 1, 2 ⊢ set x ∧ set y, conj_intro 1 2.
04. 1, 2 ⊢ fst (x, y) = x, pair_fst_conj 3.
05. 1 ⊢ set y → fst (x, y) = x, subj_intro 4.
pair_fst. ⊢ set x → set y → fst (x, y) = x, subj_intro 5.

06. 1, 2 ⊢ scd (x, y) = y, pair_scd_conj 3.
07. 1 ⊢ set y → scd (x, y) = y, subj_intro 6.
pair_scd. ⊢ set x → set y → scd (x, y) = y, subj_intro 7.

01. 1 ⊢ set x ∧ set y, hypo.
02. 2 ⊢ (x, y) = (x', y'), hypo.
03. 1 ⊢ set x, conj_eliml 1.
04. 1 ⊢ set y, conj_elimr 1.
05. 1 ⊢ set (x, y), set_pair 3 4.
06. 1, 2 ⊢ set (x', y'), eq_subst 2 5.
07. 1, 2 ⊢ set x', setl_from_pair 6.
08. 1, 2 ⊢ set y', setr_from_pair 6.
09. 1, 2 ⊢ set x' ∧ set y', conj_intro 7 8.
10. 2 ⊢ fst (x, y) = fst (x', y'), f_equal 2.
11. 1 ⊢ fst (x, y) = x, pair_fst_conj 1.
12. 1 ⊢ x = fst (x, y), eq_symm 11.
13. 1, 2 ⊢ fst (x', y') = x', pair_fst_conj 9.
14. 1, 2 ⊢ x = fst (x', y'), eq_trans 12 10.
15. 1, 2 ⊢ x = x', eq_trans 14 13.
16. 2 ⊢ scd (x, y) = scd (x', y'), f_equal 2.
17. 1 ⊢ scd (x, y) = y, pair_scd_conj 1.
18. 1 ⊢ y = scd (x, y), eq_symm 17.
19. 1, 2 ⊢ scd (x', y') = y', pair_scd_conj 9.
20. 1, 2 ⊢ y = scd (x', y'), eq_trans 18 16.
21. 1, 2 ⊢ y = y', eq_trans 20 19.
22. 1, 2 ⊢ x = x' ∧ y = y', conj_intro 15 21.
23. 1 ⊢ (x, y) = (x', y') → x = x' ∧ y = y', subj_intro 22.
pair_property_conj. ⊢ set x ∧ set y → (x, y) = (x', y') → x = x' ∧ y = y',
  subj_intro 23.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ set y, hypo.
03. 1, 2 ⊢ set x ∧ set y, conj_intro 1 2.
04. 1, 2 ⊢ (x, y) = (x', y') → x = x' ∧ y = y', pair_property_conj 3.
05. 1 ⊢ set y → (x, y) = (x', y') → x = x' ∧ y = y', subj_intro 4.
pair_property. ⊢ set x → set y → (x, y) = (x', y') → x = x' ∧ y = y',
  subj_intro 5.

01. 1 ⊢ x ∈ X, hypo.
02. 2 ⊢ y ∈ Y, hypo.
03. 1, 2 ⊢ x ∈ X ∧ y ∈ Y, conj_intro 1 2.
04. ⊢ (x, y) = (x, y), eq_refl.
05. 1, 2 ⊢ x ∈ X ∧ y ∈ Y ∧ (x, y) = (x, y), conj_intro 3 4.
06. 1, 2 ⊢ ∃b. x ∈ X ∧ b ∈ Y ∧ (x, y) = (x, b), ex_intro 5.
07. 1, 2 ⊢ ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ (x, y) = (a, b), ex_intro 6.
08. 1 ⊢ set x, set_intro 1.
09. 2 ⊢ set y, set_intro 2.
10. 1, 2 ⊢ set (x, y), set_pair 8 9.
11. 1, 2 ⊢ set (x, y) ∧ ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ (x, y) = (a, b),
  conj_intro 10 7.
12. 1, 2 ⊢ (x, y) ∈ {t | ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ t = (a, b)},
  comp_intro 11, P u ↔ ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ u = (a, b).
13. ⊢ X × Y = {t | ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ t = (a, b)}, prod_eq.
14. 1, 2 ⊢ (x, y) ∈ X × Y, eq_subst_rev 13 12, P u ↔ (x, y) ∈ u.
15. 1 ⊢ y ∈ Y → (x, y) ∈ X × Y, subj_intro 14.
prod_intro. ⊢ x ∈ X → y ∈ Y → (x, y) ∈ X × Y, subj_intro 15.

01. 1 ⊢ t ∈ X × Y, hypo.
02. ⊢ X × Y = {t | ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y)}, prod_eq.
03. 1 ⊢ t ∈ {t | ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y)},
  eq_subst 2 1, P u ↔ t ∈ u.
04. 1 ⊢ set t ∧ ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), comp_elim 3.
05. 1 ⊢ ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), conj_elimr 4.
prod_elim. ⊢ t ∈ X × Y → ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y),
  subj_intro 5.

01. 1 ⊢ (x, y) ∈ X × Y, hypo.
02. 1 ⊢ ∃a. ∃b. a ∈ X ∧ b ∈ Y ∧ (x, y) = (a, b), prod_elim 1.
03. 3 ⊢ ∃b. a ∈ X ∧ b ∈ Y ∧ (x, y) = (a, b), hypo.
04. 4 ⊢ a ∈ X ∧ b ∈ Y ∧ (x, y) = (a, b), hypo.
05. 4 ⊢ a ∈ X ∧ b ∈ Y, conj_eliml 4.
06. 4 ⊢ (x, y) = (a, b), conj_elimr 4.
07. 1 ⊢ set (x, y), set_intro 1.
08. 1 ⊢ set x, setl_from_pair 7.
09. 1 ⊢ set y, setr_from_pair 7.
10. 1 ⊢ set x ∧ set y, conj_intro 8 9.
11. 1, 4 ⊢ x = a ∧ y = b, pair_property_conj 10 6.
12. 1, 4 ⊢ x = a, conj_eliml 11.
13. 1, 4 ⊢ y = b, conj_elimr 11.
14. 1, 4 ⊢ x ∈ X ∧ b ∈ Y, eq_subst_rev 12 5.
15. 1, 4 ⊢ x ∈ X ∧ y ∈ Y, eq_subst_rev 13 14.
16. 1, 3 ⊢ x ∈ X ∧ y ∈ Y, ex_elim 3 15.
17. 1 ⊢ x ∈ X ∧ y ∈ Y, ex_elim 2 16.
prod_elim_pair. ⊢ (x, y) ∈ X × Y → x ∈ X ∧ y ∈ Y, subj_intro 17.

01. 1 ⊢ t ∈ X × Y, hypo.
02. 1 ⊢ ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), prod_elim 1.
03. 3 ⊢ ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), hypo.
04. 4 ⊢ x ∈ X ∧ y ∈ Y ∧ t = (x, y), hypo.
05. 4 ⊢ x ∈ X ∧ y ∈ Y, conj_eliml 4.
06. 4 ⊢ x ∈ X, conj_eliml 5.
07. 4 ⊢ y ∈ Y, conj_elimr 5.
08. 4 ⊢ t = (x, y), conj_elimr 4.
09. 4 ⊢ set x, set_intro 6.
10. 4 ⊢ set y, set_intro 7.
11. 4 ⊢ fst (x, y) = x, pair_fst 9 10.
12. 4 ⊢ scd (x, y) = y, pair_scd 9 10.
13. 4 ⊢ fst t = x, eq_subst_rev 8 11, P u ↔ fst u = x.
14. 4 ⊢ scd t = y, eq_subst_rev 8 12, P u ↔ scd u = y.
15. 4 ⊢ fst t ∈ X ∧ y ∈ Y, eq_subst_rev 13 5.
16. 4 ⊢ fst t ∈ X ∧ scd t ∈ Y, eq_subst_rev 14 15.
17. 3 ⊢ fst t ∈ X ∧ scd t ∈ Y, ex_elim 3 16.
18. 1 ⊢ fst t ∈ X ∧ scd t ∈ Y, ex_elim 2 17.
19. 1 ⊢ fst t ∈ X, conj_eliml 18.
prod_eliml. ⊢ t ∈ X × Y → fst t ∈ X, subj_intro 19.

20. 1 ⊢ scd t ∈ Y, conj_elimr 18.
prod_elimr. ⊢ t ∈ X × Y → scd t ∈ Y, subj_intro 20.

