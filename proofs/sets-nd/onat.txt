
(*/chapter The natural numbers as ordinals *)

onat_inductive_sets_eq. ⊢ onat_inductive_sets =
  {A | ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A}, def.
onat_eq. ⊢ onat = ⋂onat_inductive_sets, def.
succ_eq. ⊢ succ n = n ∪ {n}, def.

01. ⊢ ∃A. set A ∧ (∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A), infinity.
02. 2 ⊢ set A ∧ (∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A), hypo.
03. 2 ⊢ set A, conj_eliml 2.
04. 2 ⊢ ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A, conj_elimr 2.
05. 2 ⊢ A ∈ {A | ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A}, comp_intro 3 4.
06. 2 ⊢ A ∈ onat_inductive_sets,
  eq_subst_rev onat_inductive_sets_eq 5, P x ↔ A ∈ x.
07. 2 ⊢ onat ⊆ A, Intersection_is_lower_bound onat_eq 6.
08. 2 ⊢ set onat, subset 7 3.
onat_is_set. ⊢ set onat, ex_elim 1 8.

01. 1 ⊢ set n, hypo.
02. 1 ⊢ set {n}, set_sg 1.
03. 1 ⊢ set (n ∪ {n}), set_union 1 2.
04. 1 ⊢ set (succ n), eq_subst_rev succ_eq 3, P u ↔ set u.
succ_is_set. ⊢ set n → set (succ n), subj_intro 4.

01. 1 ⊢ A ∈ onat_inductive_sets, hypo.
02. 1 ⊢ A ∈ {A | ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A},
  eq_subst onat_inductive_sets_eq 1.
03. 1 ⊢ ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A, comp_elim 2.
04. 1 ⊢ ∅ ∈ A, conj_eliml 3.
05. ⊢ A ∈ onat_inductive_sets → ∅ ∈ A, subj_intro 4.
06. ⊢ ∀A. A ∈ onat_inductive_sets → ∅ ∈ A, uq_intro 5.
07. ⊢ ∅ ∈ ⋂onat_inductive_sets, Intersection_intro empty_set_is_set 6.
zero_in_onat. ⊢ ∅ ∈ onat, eq_subst_rev onat_eq 7, P x ↔ ∅ ∈ x.

01. 1 ⊢ n ∈ onat, hypo.
02. 2 ⊢ A ∈ onat_inductive_sets, hypo.
03. 2 ⊢ A ∈ {A | ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A},
  eq_subst onat_inductive_sets_eq 2.
04. 2 ⊢ ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A, comp_elim 3.
05. 2 ⊢ ∀n. n ∈ A → n ∪ {n} ∈ A, conj_elimr 4.
06. 2 ⊢ onat ⊆ A, Intersection_is_lower_bound onat_eq 2.
07. 1, 2 ⊢ n ∈ A, incl_elim 6 1.
08. 1, 2 ⊢ n ∪ {n} ∈ A, uq_bounded_elim 5 7.
09. 1, 2 ⊢ succ n ∈ A, eq_subst_rev succ_eq 8, P x ↔ x ∈ A.
10. 1 ⊢ A ∈ onat_inductive_sets → succ n ∈ A, subj_intro 9.
11. 1 ⊢ ∀A. A ∈ onat_inductive_sets → succ n ∈ A, uq_intro 10.
12. 1 ⊢ set n, set_intro 1.
13. 1 ⊢ set (succ n), succ_is_set 12.
14. 1 ⊢ succ n ∈ ⋂onat_inductive_sets, Intersection_intro 13 11.
15. 1 ⊢ succ n ∈ onat, eq_subst_rev onat_eq 14, P x ↔ succ n ∈ x.
succ_in_onat. ⊢ n ∈ onat → succ n ∈ onat, subj_intro 15.

01. 1 ⊢ A ⊆ onat, hypo.
02. 2 ⊢ ∅ ∈ A ∧ ∀n. n ∈ A → succ n ∈ A, hypo.
03. 2 ⊢ ∅ ∈ A, conj_eliml 2.
04. 2 ⊢ ∀n. n ∈ A → succ n ∈ A, conj_elimr 2.
05. 5 ⊢ n ∈ A, hypo.
06. 2, 5 ⊢ succ n ∈ A, uq_bounded_elim 4 5.
07. 2, 5 ⊢ n ∪ {n} ∈ A, eq_subst succ_eq 6, P x ↔ x ∈ A.
08. 2 ⊢ n ∈ A → n ∪ {n} ∈ A, subj_intro 7.
09. 2 ⊢ ∀n. n ∈ A → n ∪ {n} ∈ A, uq_intro 8.
10. 2 ⊢ ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A, conj_intro 3 9.
11. 1 ⊢ set A, subset 1 onat_is_set.
12. 1, 2 ⊢ A ∈ {A | ∅ ∈ A ∧ ∀n. n ∈ A → n ∪ {n} ∈ A}, comp_intro 11 10.
13. 1, 2 ⊢ A ∈ onat_inductive_sets,
  eq_subst_rev onat_inductive_sets_eq 12, P x ↔ A ∈ x.
14. 1, 2 ⊢ onat ⊆ A, Intersection_is_lower_bound onat_eq 13.
15. 1, 2 ⊢ A = onat, incl_antisym 1 14.
onat_induction_sets. ⊢ A ⊆ onat →
  (∅ ∈ A ∧ ∀n. n ∈ A → succ n ∈ A) → A = onat, subj_intro_ii 15.

