
wf_equi. ⊢ wf M R ↔ ∀A. A ⊆ M → ¬A = ∅ →
  ∃y. y ∈ A ∧ ¬∃x. x ∈ A ∧ (x, y) ∈ R, def.
  
wf_unfold. ⊢ wf M R → ∀A. A ⊆ M → ¬A = ∅ →
  ∃y. y ∈ A ∧ ¬∃x. x ∈ A ∧ (x, y) ∈ R, bij_eliml wf_equi.

01. 1 ⊢ A ⊆ M, hypo.
02. 2 ⊢ M \ A = ∅, hypo.
03. 2 ⊢ M ⊆ A, empty_diff 2.
04. 1, 2 ⊢ A = M, incl_antisym 1 3.
05. 1 ⊢ M \ A = ∅ → A = M, subj_intro 4.
06. 1 ⊢ ¬A = M → ¬M \ A = ∅, contraposition 5.

07. 7 ⊢ ¬A = M, hypo.
08. 1, 7 ⊢ ¬M \ A = ∅, subj_elim 6 7.
09. 9 ⊢ wf M R, hypo.
10. 9 ⊢ ∀A. A ⊆ M → ¬A = ∅ → ∃y. y ∈ A ∧ ¬∃x. x ∈ A ∧ (x, y) ∈ R,
  wf_unfold 9.
11. 9 ⊢ M \ A ⊆ M → ¬M \ A = ∅ → ∃y. y ∈ M \ A ∧ ¬∃x. x ∈ M \ A ∧ (x, y) ∈ R,
  uq_elim 10.
12. ⊢ M \ A ⊆ M, diff_incl.
13. 9 ⊢ ¬M \ A = ∅ → ∃y. y ∈ M \ A ∧ ¬∃x. x ∈ M \ A ∧ (x, y) ∈ R,
  subj_elim 11 12.
14. 9, 1, 7 ⊢ ∃y. y ∈ M \ A ∧ ¬∃x. x ∈ M \ A ∧ (x, y) ∈ R,
  subj_elim 13 8.
15. 15 ⊢ y ∈ M \ A ∧ ¬∃x. x ∈ M \ A ∧ (x, y) ∈ R, hypo.
16. 15 ⊢ y ∈ M \ A, conj_eliml 15.
17. 15 ⊢ y ∈ M, incl_elim 12 16.
18. 15 ⊢ ¬∃x. x ∈ M \ A ∧ (x, y) ∈ R, conj_elimr 15.
19. 15 ⊢ ∀x. x ∈ M \ A → ¬(x, y) ∈ R, neg_ex_bounded 18.
20. 15 ⊢ x ∈ M \ A → ¬(x, y) ∈ R, uq_elim 19.

21. 21 ⊢ ∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A, hypo.
22. 21 ⊢ y ∈ M → (∀x. x ∈ M → (x, y) ∈ R → x ∈ A) → y ∈ A, uq_elim 21.
23. 21, 15 ⊢ (∀x. x ∈ M → (x, y) ∈ R → x ∈ A) → y ∈ A, subj_elim 22 17.
24. 21, 15 ⊢ ¬y ∈ A → ¬(∀x. x ∈ M → (x, y) ∈ R → x ∈ A), contraposition 23.
25. 15 ⊢ ¬y ∈ A, diff_elimr 16.
26. 21, 15 ⊢ ¬(∀x. x ∈ M → (x, y) ∈ R → x ∈ A), subj_elim 24 25.
27. 21, 15 ⊢ ∃x. x ∈ M ∧ ¬((x, y) ∈ R → x ∈ A), neg_uq_bounded 26.
28. 28 ⊢ x ∈ M ∧ ¬((x, y) ∈ R → x ∈ A), hypo.
29. 28 ⊢ x ∈ M, conj_eliml 28.
30. 28 ⊢ ¬((x, y) ∈ R → x ∈ A), conj_elimr 28.
31. 28 ⊢ (x, y) ∈ R ∧ ¬x ∈ A, neg_subj 30.
32. 28 ⊢ (x, y) ∈ R, conj_eliml 31.
33. 28 ⊢ ¬x ∈ A, conj_elimr 31.
34. 28 ⊢ x ∈ M \ A, diff_intro 29 33.
35. 15, 28 ⊢ ¬(x, y) ∈ R, subj_elim 20 34.
36. 15, 28 ⊢ ⊥, neg_elim 35 32.
37. 21, 15 ⊢ ⊥, ex_elim 27 36.

38. 9, 1, 21, 7 ⊢ ⊥, ex_elim 14 37.
39. 9, 1, 21 ⊢ ¬¬A = M, neg_intro 38.
40. 9, 1, 21 ⊢ A = M, dne 39.
41. 9, 1 ⊢ (∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A) → A = M,
  subj_intro 40.
42. 9 ⊢ A ⊆ M → (∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A) → A = M,
  subj_intro 41.

wf_induction_set. ⊢ wf M R → A ⊆ M →
  (∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A) → A = M,
  subj_intro 42.

01. 1 ⊢ wf M R, hypo.
02. 2 ⊢ A = {x | x ∈ M ∧ P x}, hypo.
03. 3 ⊢ ∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → P y) → P x, hypo.
04. 3 ⊢ x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → P y) → P x, uq_elim 3.
05. 5 ⊢ x ∈ M, hypo.
06. 3, 5 ⊢ (∀y. y ∈ M → (y, x) ∈ R → P y) → P x, subj_elim 4 5.
07. 7 ⊢ ∀y. y ∈ M → (y, x) ∈ R → y ∈ A, hypo.
08. 7 ⊢ y ∈ M → (y, x) ∈ R → y ∈ A, uq_elim 7.
09. 9 ⊢ y ∈ M, hypo.
10. 7, 9 ⊢ (y, x) ∈ R → y ∈ A, subj_elim 8 9.
11. 11 ⊢ (y, x) ∈ R, hypo.
12. 7, 9, 11 ⊢ y ∈ A, subj_elim 10 11.
13. 2, 7, 9, 11 ⊢ y ∈ {x | x ∈ M ∧ P x}, eq_subst 2 12, P u ↔ y ∈ u.
14. 2, 7, 9, 11 ⊢ y ∈ M ∧ P y, comp_elim 13.
15. 2, 7, 9, 11 ⊢ P y, conj_elimr 14.
16. 2, 7, 9 ⊢ (y, x) ∈ R → P y, subj_intro 15.
17. 2, 7 ⊢ y ∈ M → (y, x) ∈ R → P y, subj_intro 16.
18. 2, 7 ⊢ ∀y. y ∈ M → (y, x) ∈ R → P y, uq_intro 17.
19. 2, 3, 5, 7 ⊢ P x, subj_elim 6 18.
20. 2, 3, 5, 7 ⊢ x ∈ M ∧ P x, conj_intro 5 19.
21. 5 ⊢ set x, set_intro 5.
22. 2, 3, 5, 7 ⊢ x ∈ {x | x ∈ M ∧ P x}, comp_intro 21 20.
23. 2, 3, 5, 7 ⊢ x ∈ A, eq_subst_rev 2 22, P u ↔ x ∈ u.
24. 2, 3, 5 ⊢ (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A, subj_intro 23.
25. 2, 3 ⊢ x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A, subj_intro 24.
26. 2, 3 ⊢ ∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → y ∈ A) → x ∈ A, uq_intro 25.
27. 27 ⊢ x ∈ A, hypo.
28. 2, 27 ⊢ x ∈ {x | x ∈ M ∧ P x}, eq_subst 2 27, P u ↔ x ∈ u.
29. 2, 27 ⊢ x ∈ M ∧ P x, comp_elim 28.
30. 2, 27 ⊢ x ∈ M, conj_eliml 29.
31. 2 ⊢ x ∈ A → x ∈ M, subj_intro 30.
32. 2 ⊢ ∀x. x ∈ A → x ∈ M, uq_intro 31.
33. 2 ⊢ A ⊆ M, incl_intro 32.
34. 1, 2, 3 ⊢ A = M, wf_induction_set 1 33 26.
35. 35 ⊢ x ∈ M, hypo.
36. 1, 2, 3, 35 ⊢ x ∈ A, eq_subst_rev 34 35, P u ↔ x ∈ u.
37. 1, 2, 3, 35 ⊢ x ∈ {x | x ∈ M ∧ P x}, eq_subst 2 36, P u ↔ x ∈ u.
38. 1, 2, 3, 35 ⊢ x ∈ M ∧ P x, comp_elim 37.
39. 1, 2, 3, 35 ⊢ P x, conj_elimr 38.
40. 1, 2, 3 ⊢ x ∈ M → P x, subj_intro 39.
41. 1, 3, 2 ⊢ ∀x. x ∈ M → P x, uq_intro 40.
42. 1, 3 ⊢ A = {x | x ∈ M ∧ P x} → ∀x. x ∈ M → P x, subj_intro 41.
43. 1, 3 ⊢ {x | x ∈ M ∧ P x} = {x | x ∈ M ∧ P x} → ∀x. x ∈ M → P x, 42.
44. ⊢ {x | x ∈ M ∧ P x} = {x | x ∈ M ∧ P x}, eq_refl.
45. 1, 3 ⊢ ∀x. x ∈ M → P x, subj_elim 43 44.
46. 1 ⊢ (∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → P y) → P x) →
  (∀x. x ∈ M → P x), subj_intro 45.

wf_induction. ⊢ wf M R →
  (∀x. x ∈ M → (∀y. y ∈ M → (y, x) ∈ R → P y) → P x) →
  (∀x. x ∈ M → P x), subj_intro 46.

